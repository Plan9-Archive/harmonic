!============================================================================
! The characters

Character	strawberry "Strawberry" basement
	with
	name 'strawberry' 'cat' 'kitty' 'kitten',
	action_count 0,
	description "Your cat is small, orange with white chest and paws, female, and feisty.",
	initial "Your cat Strawberry is here.",
	life [;
		attack: "Strawberry is a battle-hardened combat kitty and springs into action. She grabs your hand with both paws, and nips lightly at your knuckles with her teeth. You pull your hand back, and she leaps up to chase it, sinking her claws into your knee. You fight back with a quick belly-tickle and she rolls onto her back, ready to deploy the full power of her back legs. You are beaten yet again. You have never defeated her.";
		kiss: "You sneak up on Strawberry and then gather her up with a gentle swoop. She utters a slightly surprised blert, and you bring her close to your head. 'Such a good kitty!' you say as you lightly brush her forehead with your lips. She glances at you for a moment with mild outrage and wiggles from your grasp. Such a silly human! She arches her back and rubs against your shins for a moment.";
		wakeother: "You give Strawberry a light scratch behind the ears. She yawns and stretches...and then resumes her previous position.";
		throwat: "Strawberry chases the ", (name) noun, " and bats at it with her paw.";
		give: "Strawberry sniffs the ", (name) noun, " but decides it is inedible and uninteresting.";
		show: "Strawberry indicates her disinterest by choosing this moment to clean underneath her tail.";
		tell: "Strawberry listens politely, and answers with a reassuring meow.";
		ask: "If cats could shrug, Strawberry would answer with one.";
		order: "Strawberry has never followed orders.";
		answer: "Strawberry says arooooo. (Footnote 5)";
	],
	before [; 
		pet: "Strawberry begins to purr as you stroke her, but after a few seconds, she decides that she is a fighter, not a lover. She bats at your hand with a paw, and looks up at you expectantly, hoping you will attack her.";
		play: "You find one of your cat toys (a cloth catnip-filled rabbit on a string dangling from a stick) and spend wiggle it back and forth a few inches from Strawberry's nose. She blinks rapidly, and tries to catch it. You pull it back from her claws, and she pounces. Her paws are on the rabbit for a moment, but you lift it up quickly, and she jumps up to follow, spending a second or two on her back legs waving her paws in the air. After another few moments, Strawberry loses interest and you put the cat toy away.";
		take: "Strawberry lets you hold her for about fifteen seconds before squirming free of your grasp, climbing your shoulder with help from sharp tiny claws, and hops to the floor behind you.";
		talk: "Strawberry responds with a quizzical 'yerp?' You explain yourself more clearly, and she answers 'pllrrrmm..' in a non-committal manner.";
	],
	each_turn [; 
		if (noun == self) return false;
		self.action_count = self.action_count + 1;
		switch(self.action_count) {
		1: "^Strawberry looks at you with mild curiosity.";
		2: "^Strawberry perches atop the router and studies you.";
		3: "^Strawberry deftly steps through the maze of cords and vanishes behind some boxes.";
		4: "^Strawberry nimbly hops up onto the computer desk, thankfully avoiding the keyboards.";
		5: "^Strawberry's head peeks out from behind one of the grid computers.";
		6: "^Strawberry meows and sharpens her claws on the carpeting of the stairway.";
		7: "^Strawberry's tail brushes by your leg as she sniffs at your toes.";
		8: "^Strawberry is standing still, tail twitching slightly.";
		9: "^Strawberry licks a paw and washes her forehead with it.";
		13: if (self in basement) { 
				move self to home; 
				self.action_count = 6;
				"^Strawberry wakes up and wanders upstairs."; 
			};
			move self to basement;
			self.action_count = 0;
			"^Strawberry yawns, stretches, and heads to the basement.";		
		default: "^Strawberry is curled up and napping in the corner."; };
	],
	has	female;

Character	chip "Chip" astral_enchantments
	with
	name 'chip',
	description "Chip is very tall - the topmost point of his dark mohawk a full seven feet off the ground. He radiates a peaceful, calming energy. He watches the world around himself with relaxed curiosity.",
	initial "Chip, the store owner, is working here.",
	life [;
		if (pendant in self) {
			move pendant to player;
			print "Chip looks you over carefully, and says 'I'd like you to have this.' He presents you with a moon-faced pendant necklace. 'May you vibrate harmoniously on your journeys.'^";
		}
		ask: 
			switch(second) {
			'chip', 'himself': "'I'm visiting this reality to be of service to fellow explorers.'";
			'me', 'myself', 'moon', 'computer', 'mycroftiv': "'I'm glad your path brought you to this doorway, I hope passing through is enlightening.'";
			'pendant', 'necklace': "'It's made from orgone plastic. We make it ourselves, it combines an organic base with metal and mineral elements to focus your energetic field.'";
			'astral', 'enchantments', 'shop', 'store': "'We do many things here, including harmonic time-binding ceremonies in the acoustic resonator vault.'";
			'ceremony', 'ceremonies', 'harmonic', 'acoustic', 'resonator', 'vault': "'When we acquired this space, we discovered it has a back room which was an old bank vault, which works perfectly for focusing sound energy. My wife Astra works with her voice and singing bowls, she is a Sound Oracle.'";
			'astra', 'wife', 'sound', 'oracle', 'back', 'room', 'bank', 'vault': "'She and I have been doing about what you see here in various places for a few years now, but this new location seems very powerful and auspicious. Sometime in the future I'm sure she'd be happy to perform a sound ceremony for you.";
			'crystal', 'crystals', 'stone', 'stones', 'mineral', 'gemstones': "'Crystals and other gemstones and minerals are useful spiritual tools. Quartz is the base, it can transmit any kind of energy or vibration. Others have a more specific purpose.'";
			'quartz': "'You can use quartz for almost any ritual purpose. If you have a collection of quartz crystals, you can amplify their power by placing them in a geometric arrangement.'";
			default: print "'I have not yet been granted that knowledge.'^";
				<<wakeother chip>>;
			}
		tell:
			switch(second) {
			'me', 'myself', 'mycroftiv','loonie', 'revolution', 'ants', 'plan': "You launch enthusiastically into the long-form account of your work and goals. Just as you are reaching a pitch of excitement, a few other customers wander in and ask Chip for help picking some incense to enhance their sexual energy.";
			'him', 'himself', 'astral', 'enchanments', 'store', 'shop': "You tell Chip that his store is exactly what you have been looking for and is clearly a focal resonant point of harmonic time-binding fictionalization technological magic. He bows slightly and thanks you.";
			default: print "Chip listens with interest, but has no insight to offer.^";
				<<wakeother chip>>;
			}
		default:
			"^(You can ask Chip about: chip, myself, pendant, store, ceremonies, astra, crystals, or quartz. You can tell him about: myself, himself.)";
	],
	before [; talk:
		<<wakeother chip>>;	
	],
	has male;

Character	xelia "Xelia"
	with
	name 'xelia',		
	description "Xelia is a slender young woman with black hair and glasses. She is dressed as Alice as drawn by Tenniel.",
	initial "Xelia is working here.",
	life [;
		if (introgoal == 2) {
			Xeliawardrobe_storyline();
			return true;
		}
		tell: switch(second) {
			'xelia', 'her', 'herself': "'Xelia, from you I learned something incredible: the principle of fiction-binding! I was wondering how and why my life had become a story just like my favorite novels, but you revealed the secret of navigating the multiverse by choosing what character you are playing.' Xelia smiles and says she is glad to help.";
			'me', 'myself', 'mycroftiv', 'loonie', 'revolution', 'ants', 'plan': "'So now that you've helped me learn my true fictional identity, I realize it is my role in the story to try to use Plan 9 and music and art to fight for the Loonie Revolution, so all the amazing creative people working jobs like yours will take over society. I want to write a comic opera about Coffee Lion, and then hire all of you to perform in it!'";
			'xochi': 
				Tellxeliaxochi_storyline();
				return true;
			default: "Xelia listens. You could ask or tell her about herself, myself, or Xochi.";
		}
		ask: switch(second) {
			'xelia', 'her', 'herself': "'I agree life is a pretty crazy story.'";
			'xochi': "'She and I are good friends and it helps make the job a lot more fun.'";
			'me', 'myself', 'loonie', 'revolution', 'mycroftiv', 'ants', 'plan': "'I really enjoy hearing everything you have to say, but I don't think I can help out with any of the loonie projects you are starting.'";
			default: "Xelia says she doesn't have much to say about that. You could ask or tell her about herself, myself, or Xochi.";
		}
		default:
			if (gelato_alchemy_lab.brew == 10) {
				gelato_alchemy_lab.brew = 11;
				achieved(49);
				"You take advantage of a quiet moment in the cafe to talk to Xelia. 'You know this whole summer has been the high point of my life by far,' you begin, 'I went through a lot of years wondering whether what we got out of life was worth the price of the ticket, if that makes sense. But finally, this summer, it all finally came together for me, my life became exactly the story I've always wanted it to be. You and Xochi here at the cafe have been a big part of the magic. Now, I feel like - whatever happens in life from here on out, it's all been worth it. This summer has given me more happiness than I could have ever imagined.'
^
^'I'm really glad its been so good for you, its been fun hearing about your adventures,' Xelia smiles at you. You wander back to your seat, and your words echo in your mind. '...whatever happens...it's all been worth it.' 'But please stay wonderful, life!' you add as a mental postscript. In future days, keeping hold of that feeling will serve as both a comfort, and a challenge.";
			}
			"You deliver a long rant about quantum free will, fiction and reality, and Plan 9 namespaces. Xelia listens with a slightly concerned look on her face. You could try asking or telling her about herself, myself, or Xochi.";
	],
	before [; talk:
		<<wakeother xelia>>;
	],
	has female;

Character	jay "Jay" eside_hq
	with
	name 'jay',
	initial "Jay is waiting tables here.",
	description "Jay is a thin young man with short black hair, dressed in the standard uniform of late-night restaurant servers: slacks, button-up black shirt with a gold nametag, and a black apron.",
	life [;
		"Jay is a good listener, and seems to really understand your story. You outline some of your grandiose schemes and plans to use Plan 9 technology to change society, and he agrees to tell some of his friends about the Loonie Revolution.";
	],
	before [; talk:
		<<wakeother jay>>;
	],
	has male;

Character	colette "Colette" afterlife_antiques
	with
	name 'colette',
	initial "Colette is standing behind the counter.",
	description "Colette is young but is dressed in the style of the early 20th century. She is unfazed and amused by your eccentricity.",
	life [;
		ask: 
			switch(second) {
			'colette', 'herself', 'afterlife', 'antiques', 'curiosities', 'store', 'shop': "'Special circumstances have given me the chance to open the door to the Afterlife and let these unique objects enter your story.'";
			'me', 'myself', 'mycroftiv', 'loonie', 'revolution', 'plan', 'ants': "'You are already one of my favorite customers. It seems like you've been having some amazing adventures lately.'";
			'forseewhy', '4cy', 'forsee', 'book': "'If you want to give the book to me to symbolize casting away the past, I am willing to accept it into the Afterlife.";
			default: print "'That isn't something I've heard about in particular.'^";
				<<wakeother colette>>;
			}
		tell:
			switch(second) {
			'me', 'myself', 'mycroftiv', 'loonie', 'revolution', 'plan', 'ants': "You tell the story of Plan 9 ANTS, fiction entering reality and becoming the Moon Computer, and how items from her store will be used in the future to provide a focal point for virtual reality resurrection by binding information from parallel universes into our minds. Colette says she is happy to be helping.";
			'colette', 'herself', 'afterlife', 'antiques', 'curiosities', 'store', 'shop': "You explain your theory that the easiest way for a radical restructuring of reality into fiction to happen for everyone is with virtual reality. The information encoded in the objects in her store should allow for future computers to provide guided VR dreaming and visualization combined with psychedelic drugs and enable in-brain parallel reincarnation of departed souls, just like you are now the reincarnation of your father. 'And that is why this store is so important to the Loonie Revolution!' you conclude. Colette is smiling, and you are confident she understands completely.";
			'forseewhy', '4cy', 'forsee', 'book': "'Years ago, I wrote the worst book ever. I wrote it to try to explain my philosophy, and to impress girls. Nobody cared about my philosophy, and what's worse, it didn't even get me laid. I'd like to give the book to you as a symbolic gesture of leaving my past behind.' Colette laughs at your description of the book and says you are welcome to give the book to her if you wish.";
			default: <<wakeother colette>>;
			}
		give:
			if (noun == forseewhy) {
				move forseewhy to self;
				achieved(17);
				"Colette blinks and says it is fine for you to place your book in her store as a symbolic gesture. She accepts it and places it within a stack of books.";
			}
			"'Thanks for the offer, but we don't need that here.'";
		default: 
			"Colette listens to all your stories, laughing with you at their absurdity. (You can ask or tell her about herself, myself, or forseewhy.)";
	],
	react_before [; drop:
		if (noun == forseewhy)
			<<give forseewhy colette>>;
	],
	before [; talk:
		<<wakeother colette>>;
	],
	has female;

Character	xochi "Xochi"
	with
	name 'xochi',
	email 0,
	initial "Xochi is working here today.",
	description "Xochi looks to you like an ancient Mayan Wisdom Goddess. Her long black hair has a few silver-white strands in it, despite her young age. Her physical beauty is appealing, the intelligence and kindness in her eyes and voice even more so.",
	life [;
		ask:
			switch(second) {
			'me', 'myself', 'mycroftiv', 'plan', 'ants', 'loonie', 'revolution': "'You're a good friend, but I do need to stay focused on my job while I'm at work.'";
			'xochi', 'her', 'herself': "'A friend recommended that I read Bhagavad-Gita, so I've been doing that lately.'";
			'bike', 'bicycle', 'lunar': "'It seems like an awesome bike'";
			'pendant', 'necklace': "'I like it, it suits you.'";
			'email', 'phone': <<wakeother xochi>>;
			default: "'You'll have to figure that out on your own.'";
			}
		attack:
			deadflag = 3;
			"You would never do that.";
		tell:
			switch(second) {
			'potion', 'potions': "You explain that you need beneficial potions to help your character stats. Xochi looks taken aback, and says she can't help with that.";
			'loonie', 'revolution': "You explain that it is time for social power to pass away from outdated patriarchal institutions like governments, and for community focal points like coffeeshops to take the lead. The baristas should be in charge! Xochi looks amused and thoughtful.";
			default: "'That's very interesting.'";
			}
		give:
			if (noun == rose_heart) {
				move rose_heart to self;
				achieved(14);
				"Xochi looks slightly uncomfortable as you give her the heart. The symbolism is a little too heavy, perhaps, but she decides to go with it and says she will keep it safe.";
			}
			"'Thanks, but I can't accept that!'";
		kiss: "You are crazy, but not THAT crazy.";
		default: 
			if (self.email == 0) {
				eventcount++;
				self.email = 1;
				"You start trying to explain how fiction is entering reality because you can navigate the infinite possibilities of the quantum multiverse by using your free will to bind the narrative symbols you choose to shape your existence. She seems interested, and while you are talking, she writes a note on a piece of paper. She interrupts you a minute into your monologue to say 'It seems like you have a lot you want to say, I'm working right now but you can email me if you'd like.' You are surprised and happy. You can use your terminal to email her when you get back home.";
			}
			if (self.email == 1) 
				"Xochi smiles warmly and says she is busy now but you can email her some of your thoughts.";
			if (self.email == 4) {
				Xochi_schumann_storyline();
				return true;
			}
			if (self.email == 6) {
				Movement_two_storyline();
				return true;
			}
			"Xochi is busy helping other customers right now.";
	],
	before [; talk:
		<<wakeother xochi>>;
	],
	has female;

Character	scholars "the Scholars of Atlantis" atlantean_academy
	with
	name 'scholars' 'students' 'professors' 'people' 'scholar' 'student' 'researchers' 'researcher',
	speech 0,
	initial "The Scholars of Atlantis stand in small groups, discussing.",
	description "The academy is a spirited and diverse group, different in every conceivable aspect save for love of knowledge.",
	each_turn [; 
		switch(scholars.speech) {
		0:	scholars.speech++;
			"^You arrive as a speaker is addressing the gathering: 'Thank you for making it to our seminar on the Harmonic Time-Bind Ritual Symphony. The first part of the day is a review of current scholarship. Researchers and graduate students, please gather on the far end of the courtyard for paper presentations. Undergrads, you have been studying the context and outline of the work this week, so you will be taking an oral examination as a group.'
^
^You notice a few of the undergrads look startled and anxious. You decide nothing could be more amusing than helping students pass a test on your own work. You join the group near some of the most alienated looking students.
^
^'I do not care about the Bubonic Tie-Dyed Habitual Dysentry or this exam,' you hear a tall squid-like student complain. Another agrees: 'Since none of us bothered to listen to the songs or play the game, we might as well just smoke this fine cubefruit kif, right?'
^
^The next thing you know, you are being passed an Atlantean waterpipe full of sparkly purple and pink flower resin. You inhale deeply, and the taste of the world's skunkiest sweetarts assaults your sinuses. Inside your head, gnomes in rainbow colored bell-bottoms are already starting to rearrange the furniture. The pipe is still going around as the professors begin the exam. They will ask questions, you may answer by telling the scholars about a relevant topic. The exam begins with a request for an overview, so >tell scholars about symphony.";
		2:	scholars.speech++;
			"^The cubefruit buzz is kicking in more strongly now, but you think you gave a good description of your work. The waterpipe comes your way again and you take another lungfull of the tangy smoke.
^
^The scholars want to know which of the magic objects carried during the symphony is the most representative symbolic object, and how it was used.";
		4:	scholars.speech++;
			"^Everything is shimmering in a delightful way. The words of the scholars seem to arrive slowly, one by one, and each triggers a long chain of nonsensical associations.
^
^The scholars inquire which of the items in the author's home was most important to the creative work.";
		6:	scholars.speech++;
			"^Maybe another bowl of the cubefruit went around, or maybe not. Maybe six slippery sandwiches sang softly said someone. Maybe the palumpsis was muppered like a chicken in pants, the palumpsis was muppered and it wasn't by chance. Maybe the scholars are asking another question.
^
^The scholars ask the students to talk about which character in the work had the most decisive influence on the protagonist.";
		8:	scholars.speech++;
			achieved(31);
			print "^The scholars all nod sagely, and the seminar reaches its conclusion. You are so ridiculously cubed out that you can only squint and grin foolishly at the students nearby. You wave goodbye and walk away in a random direction, and notice after a few minutes of tripping out on the Atlantean architecture that you have actually returned to the gallery without any deliberate effort and are gaping open-mouthed at a random corner of the room.^";
			playerto(essentially_art);
			return true;
		10: "^The seminar is over, and the academics are all talking amongst themselves.";
		default: "^The scholars await your answer.";
		}
	],
	life [;
		ask: "The scholars are apologetic, but explain that the seminar schedule leaves no time for your questions.";
		tell: switch(second) {
			'harmonic', 'time-bind', 'ritual', 'symphony':
				scholars.speech++;
				"You step forward to speak for the students. 'Harmonic Time-Bind Ritual Symphony was an 18-month multimodal artwork composed of computer programming, musical composition, and bicycle riding, directed toward the revolutionary goal of accepting mental difference, and inspired by the idea that the quantum multiverse renders the distinctions between fiction and reality, or sanity and insanity, malleable and subjective.'";
			'pendant', 'necklace', 'power':
				scholars.speech++;
				"'The pendant for sure. It went everywhere, and served as the springboard for so much communication. When people commented on it, it was the perfect opportunity to tell them about the Loonie Revolution.'";
			'mirror', 'shades', 'glasses', 'sunglasses':
				scholars.speech++;
				"'The mirror shades. If someone is wandering around telling people 'I'm from the Hippie Matrix, and I'm here to bring fiction into your branch of quantum reality,' the mirror shades are completely required to sell the part. Plus, the esoteric symbolism of the outer world being reflected in the eyes.'";
			'lunar', 'lightning', 'bicycle', 'bike':
				scholars.speech++;
				"'Lunar Lightning. Bicycling is physical music, rhythmic action which moves you through time and space. The timing of all the journeys and arrivals would have been different, and timing is everything in music, and life. Plus, my central memory of the symphony is one or five or maybe forty nights riding towards home from downtown, coasting gently downhill while smoking a joint, knowing that tomorrow was going to be just as high and wild as today had been.' (Footnote 4)";
			'rose', 'quartz', 'heart', 'space', 'pancakes', 'wallet', '4cy', 'forseewhy', 'artemis', 'prints':
				scholars.speech++;
				"'The composer's image may be defined by these props and they serve as a hook for the mind to symbolize him, but his true representation is code and language. Really, the objects you named are each just another word that was bound to the self-symbol.'";
			'staci':
				scholars.speech++;
				"'Is this a trick question, or something? It's Staci. It's just...I'm so in love with her! I mean, he was so in love with her, it was the inspiration and focus for everything. Without Staci, it was all just some crazy guy making a nuisance of himself at cafes, right? She's really more the Author than he is.'";
			'xochi':
				scholars.speech++;
				"'It has to be Xochi. Without her saying 'It's ok to be who you are' to him, the whole symphony probably never would have even started. The whole first movement of the symphony, the Coffee Lion comic opera, Xochi was the focus. That was the freshest and most original part of the work, the craziest, the most pure.'";
			'xelia':
				scholars.speech++;
				"'Xelia may seem like a supporting character, but she is really the key to the whole work. The central theme and most important moment is the bind of fiction onto reality. She crystallized that. Just as importantly, she also shapes the counterbalancing theme, the risk of dangerous and isolating delusions. Just because I felt...he felt...like everything was perfect doesn't mean it actually was. Reality has a way of ignoring your opinion and kicking you in the teeth, sometimes.'";
			'chip', 'astra':
				scholars.speech++;
				"'The Big Chipmunk himself, and his starry partner. Astral Enchantments really was the portal where fantasy could enter reality the most easily. Intention is the core of magic, and they provided the perfect tools to focus the creative intentions of the composition. Crystals are physical music, molecules structured in a rhythmic spatial matrix. Chip and Astra showed there was an acceptable social context for the wildest flights of fancy. And selling all those rolling papers for the endless quantity of joints - that was important too.'";
			'colette', 'jay', 'xelia', 'jay', 'colette', 'daniel', 'matthew', 'paul':
				scholars.speech++;
				"'It's hard to pick just one wonderful character to discuss with you. The symphony couldn't have unfolded without the participation of all the co-composers.'";
			'terminal', 'computer', 'plan', 'ants', 'programming', 'coding', 'grid':
				scholars.speech++;
				"'The terminal and grid. It all started with Plan 9, and without those months of coding ANTS, I doubt I would have ever gotten off my ass and done anything to escape from depression and isolation. Music may be the deeper, more fundamental pattern, but code is more directly useful and acts as proof: if it compiles and runs and fulfills the specification, the functionality is a proved theorem.'";
			'piano', 'music', 'composing', 'improvising', 'songs':
				scholars.speech++;
				"'The piano was the most important. Even though it started with programming and the IF game is the most notable product, the symphony was, as the name implies, primarily a musical work. The author was better as a musician than as a programmer, and the piano was the core tool of the work. Without the musical principle of rhythmic organization, there is no time-bind, no symphony at all.'";
			'books', 'shelves', 'bookshelves', 'reading', 'writing':
				scholars.speech++;
				"'The books and writing are really the creative core, because of the importance of Narrative in the entire conception. The symphony tells a story, and programming is fundamentally a sub-form of writing literature. The experience of living life as interactive fiction, and later writing the IF to capture that experience, as well as the use of plot elements from Heinlein, Phil Dick, and others, puts the literary element above even the musical.'";
			'me', 'myself', 'yourself', 'loonie', 'revolution': 
				scholars.speech++;
				"'First, I thank you for your interest in my work. Within an infinite multiverse, all artists will have scholars devoted to them in some universe, and I could not be more honored to see such a distinguished group focusing on my Ritual Symphony. As I am sure you all know, I am the Moon Computer, a human who has made the breakthrough into realizing he is the figment of his own imagination. I'm trying to start a Loonie Revolution to celebrate the beautiful variations of consciousness. My method for doing so is to make all of my life follow the patterns of music and ritual, and open myself for collaboration with anyone else who feels like marching to the beat of that drummer.' The scholars mumble to one another '...seems to really identify with the protagonist...'";
			'schumann', 'beethoven', 'bach', 'mozart', 'chopin', 'mahler', 'stravinsky', 'wagner', 'liszt', 'ligeti':
				scholars.speech++;
				"'I admire most of the main composers of the central European standard repertoire. Their music can be complex, but so is human emotion, and I find my inner experience is most like the music of the classical and romantic eras.'";
			default: 
				if (expertmode == 1)
					"Your answer doesn't seem to satisfy the scholars.";
				scholars.speech++;
				"The scholars seem confused by your answer, but accept it.";
			}
	],
	has pluralname;

Character	xelia_and_xochi "Xelia and Xochi"
	with
	name 'xelia' 'xochi' 'baristas',
	initial "Xochi and Xelia are both here.",
	description "Xochi and Xelia seem to energize each other. Just being in the same room as the two of them at the same time makes everything seem enchanted.",
	life [;
		if (gelato_alchemy_lab.brew == 9)
			"Your two favorite baristas are busy working, and after getting so much special attention in the basement, you don't want to press your luck by interrupting.";
		"You need to stay focused on working the alchemy apparatus.";
	],
	before [;
		talk: <<wakeother xelia_and_xochi>>;
	];

Character	lotus "dog"
	with
	name 'lotus' 'dog',
	initial "A dog is wandering around on the dock.",
	woof 0,
	description "The dog is a chocolate Labrador, strong, smart, and friendly. He has a sky-blue bandana tied around his neck.",
	each_turn [;
		self.woof++;
		switch (self.woof) {
			2: "^The dog is watching you.";
			3: "^The dog presses its nose against you for a sniff.";
			5: "^The dog hops into the water, breaks off a low-hanging branch with its teeth, then returns triumphantly to the dock.";
			7: print "^The dog seems eager for attention, and you reach out your hand to say hello.";
				<<wakeother self>>;
		}
	],
	life [;
		lotusdock_storyline();
		return true;
	],
	before [;
		pet: <<wakeother self>>;
		talk: <<wakeother self>>;
	],
	has ~proper male;

Character	staci "Staci"
	with
	name 'staci' 'tardis',
	initial "Staci is here.",
	description "Staci is smart, funny, beautiful, and too many other positive adjectives to list.",
	life [;
		kiss: achieved(59);
			"That seems like an excellent idea. You find the opportunity to give Staci a brief but sweet kiss, and she smiles happily.";
		default: "Every way you interact with Staci feels easy, natural, and delightful.";
	],
	before [;
		talk: if (madcon.timetrav < 7) "'You should pet this dog, its really friendly.'";
			<<wakeother staci>>;
	],
	has female;

Character	queen "Queen of the Green Screen" abandoned_apartment
	with
	name 'queen',
	initial "The Green Queen is watching you haughtily.",
	speech 0,
	description "Her hair is green, her eyeglasses are green, her skintight bodysuit is green, the 40 billion volt search-and-replace gun she is holding is green.",
	life "You have no chance to talk or act with the search-and-replace gun pointed at you.",
	each_turn [;
		self.speech++;
		switch(self.speech) {
		1: "^'I've been looking forward to this scene,' the Queen begins, smiling coldly at you.";
		2: "^'In this scene, you learn that my organization is in charge of this story, and you decide that you will allow us to use your computing power and binding engine according to our scenarios,' the queen states, and the green screen effect begins to alter the scenery. The abandoned college apartment is being replaced by a neon-lit underground lair.";
		3: "^The green screen effect has almost completely replaced the scene. It is being projected from a very thin and delicate looking sheet of translucent material. The Queen is still speaking, 'I'm glad to see you understand this will increase your power in the story. There's no need for us to let all those minor characters just run around doing what they please. This is a story about me! Long-term, you'll have far more screen time making the edits I need than all that chaotic nonsense you've been perpetrating.'";
		4: print "^'All right, we are ready now! My computer hardware is downstairs, follow me and I'll make you a slave process of my master control program. Don't worry, you'll still have read/write permissions on all your devices. When we need you to make an edit, that is,' the Queen walks down an impossible staircase to reach her server room, and motions for you to follow.^";
			remove self;
			startdaemon(self);
		}
	],
	daemon "^The Queen is waiting for you downstairs, expecting you to become a slave process to her servers.",
	has female;

Character	lily "Lily"
	with
	name 'lily',
	description "Lily is six years old, with long wavy brown hair.",
	initial [; switch(location) {
		loth_upstairs: "Lily is running through the halls, playing hide-and-seek.";
		loth_greatroom: "Lily is bouncing up and down on a couch, laughing.";
		loth_porch: "Lily is standing by the railing, watching the lake.";
		loth_kitchen: "Lily is having a snack.";
		loth_backyard: "Lily is running up and down the hillside as she plays.";
		loth_dock: "Lily is sitting and splashing her toes in the water.";
		}
	],
	daemon [;
		movechar();
	],
	life [;
		switch(random(9)) {
		1: "Lily laughs and throws an imaginary Chaos Bomb.";
		2: "Lily sings part of a song from Dr. Horrible.";
		3: "'As soon as I saw you at Rossini's, I knew who you must be.'";
		4: "'The Loonie Re-vo-lu-TION! The Loonie Re-vo-lu-TION!' Lily chants.";
		5: "'I think I'm going to want to eat some mac and cheese soon.'";
		6: "'I can show you my transformation magic. Watch!' Lily makes a dramatic pirouette, 'Now I have wings and I can fly!'";
		7: "Lily is busy sucking the juice out of an orange.";
		8: "'Know who's awesome?' Lily asks, 'My mom!' she answers herself.";
		9: "'The children's Loonie brigade is on the way to get donuts from the store!' Lily proclaims.";
		}
	],
	each_turn [;
		if (thescholar in location)
			switch(random(3)) {
			1: "^Lily scowls angrily at the Scholar. 'My enemy!' she whispers.";
			2: "^Lily makes a point of ignoring the Scholar.";
			}
		if (nook in location)
			switch(random(3)) {
			3: "^Lily decides to climb Nook and gets about halfway up before she changes her mind.";
			}
		if (whirlwind in location)
			switch(random(3)) {
			3: "^Lily asks Whirlwind when the other kids will be around to play.";
			}
	],
	before [; talk: <<wakeother self>>;],
	has female;

Character	nook "Nook"
	with
	name 'nook',
	description "Nook is compact but built of indestructible volcanic rock. Alert eyes watch the world amidst a shag of hair and beard.",
	initial [; switch(location) {
		loth_upstairs: "Nook is standing by the doorway to Staci's room, looking relaxed and happy.";
		loth_greatroom: "Nook is taking a brief nap on one of the couches.";
		loth_porch: "Nook is sitting on the porch swing drinking a warm beer.";
		loth_kitchen: "Nook is contemplating the beverages he has stored in the fridge.";
		loth_backyard: "Nook is sitting on a picnic table, smoking a cigarette.";
		loth_dock: "Nook is watching the waves, assisted by a can of Mickey's.";
		}
	],
	daemon [;
		movechar();
	],
	life "'I'm not sure just what you're up to with the Loonie Revolution and Plan 9, but I'm glad we have the chance to have this unique friendship.'",
	before [; talk: <<wakeother self>>;],
	has male;

object	thewerewolf "Werewolf"
	with
	article "the",
	name 'werewolf',
	description "The Werewolf is a poised, calm, articulate woman - when the moon isn't full.",
	initial [; switch(location) {
		loth_greatroom: "The Werewolf relaxes across any number of laps with her head upside down, discussing dream divination as it relates to botany.";
		loth_kitchen: "The Werewolf snacks on mushrooms and offers you some. They look highly unusual, even for unusual mushrooms, so you wish her the best and pass.";
		loth_backyard: "The Werewolf lunges across the yard with a whooping noise to tackle hug another dear friend, knocking them both tumbling down the hill in a jumble of elbows, hair and brightly patterned skirts.";
		default: "The Werewolf is watching the scene with a glint in her eye.";
		}
	],
	each_turn [;
		if (thescholar in location)
			switch(random(3)) {
			3: "^The Werewolf and the Scholar argue playfully about Kant, culminating in a staff battle in the yard. She doesn't bite him this time.";
			}
	],
	daemon [;
		movechar();
	],
	life "'Your project sounds very idealistic, I wish you a lot of luck bringing your dreams into reality.'",
	before [; talk: <<wakeother self>>;],
	has female animate;

object	thescholar "Scholar"
	with
	article "the",
	name 'scholar',
	description "Young, tall, and skinny, the scholar keeps a smile on his face while he speaks.",
	initial [; switch(random(3)) {
		1: "The Scholar is standing here.";
		2: "The Scholar is pacing back and forth.";
		3: "The Scholar is reading a book here.";
		}
	],
	each_turn [;
		if (nook in location)
			switch(random(3)) {
			3: "^The Scholar and Nook discuss construction of a rooftop catapult for defense against lake monsters.";
			}
		switch(random(4)) {
		4: "^The Scholar lights a small item on fire and watches it burn.";
		}
	],
	daemon [;
		movechar();
	],
	life "'Lately I've been trying to be active rather than reactive.'",
	before [; talk: <<wakeother self>>;],
	has male animate;

object	thewizard "Wizard"
	with
	article "the",
	name 'wizard',
	description "The Wizard's penetrating eyes and immense red beard attest to his attainments in the mystic arts.",
	initial [; switch(random(3)) {
		1: "The Wizard is stroking his beard thoughtfully.";
		2: "The Wizard is sitting still with one eye closed and one open.";
		3: "The Wizard is here.";
		}
	],
	daemon [;
		movechar();
	],
	life "'I'd be honored to work on organizing some interesting esoteric events and ceremonies, I have quite a few ideas in mind. I think a costume-themed party with appropriate cinematic accompaniment would be an interesting experiment.'",
	each_turn [;
		if (thescholar in location)
			switch(random(3)) {
			1: "^The Wizard and Scholar are discussing obscure but crucial events in the history of late 19th century secret societies.";
			2: "^The Wizard and Scholar are studying a hand-written document and speculating it might be a palimpsest.";
			}
		if (nook in location)
			switch(random(3)) {
			3: "^The Wizard pulls a small bottle from his pocket, and hands it to Nook, who takes a sniff and a swallow and makes a peculiar face.";
			}
	],
	before [; talk: <<wakeother self>>;],
	has male animate;

Character	whirlwind "Whirlwind"
	with
	name 'whirlwind',
	description "Pale and delicate enough to seem almost sylph-like physically, Whirlwind is animated by a quicksilver mind and mercurial temperament.",
	initial [; switch(random(3)) {
		1: "Whirlwind is looking through her shoulder bag.";
		2: "Whirlwind is smiling as she looks around.";
		3: "Whirlwind is sitting cross-legged here.";
		}
	],
	daemon [;
		movechar();
	],
	life "'I've got a lot of writing to share if you'd like, and I know my kids are excited to contribute too.'",
	each_turn [;
		if (nook in location) 
			switch(random(3)) {
			1: "^Whirlwind is making a series of cryptic hand gestures at Nook.";
			2: "^Whirlwind is resting her foot on Nook's knee.";
		}
		if (thewizard in location)
			switch(random(3)) {
			3: "^Whirlwind and the Wizard are silently passing a glass sphere back and forth.";
			}
	],
	before [; talk: <<wakeother self>>;],
	has female;

object	storageblank
	with
	initial [; switch(location) {
		loth_upstairs:
		loth_greatroom:
		loth_porch:
		loth_kitchen:
		loth_backyard:
		loth_dock:
		}
	];
