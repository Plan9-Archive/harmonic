!============================================================================
! The game locations

Room    milw_street "Outside my house"
	with	
	name 'milwaukee' 'street',
	description "Your section of Milwaukee Street is directly across from a small farm which remains as an agricultural island within the city. An open field and a barn are across the street. Toward the southwest is Atwood Street leading toward downtown. Monona Drive is to the south. The edge of town lies toward the northeast.",
	before [;
		go: 
			if((introgoal == 0) || (introgoal == 2))
				if((noun ~= s_obj) && (noun ~= e_obj) && (noun ~= in_obj))
					"You feel compelled to go south to Coffee Lion Cafe for coffee instead.";
			if(introgoal == 1)
				if(noun ~= ne_obj) 
					"You feel compelled to go northeast to the 24-hour restaurant instead.";
			if(introgoal == 3)
				if((noun ~= e_obj) && (noun ~= in_obj)) 
					"You feel compelled to go inside and investigate your grid hardware in the basement instead.";
	],
	in_to home,
	e_to home,
	sw_to atwood_av,
	s_to mon_cot,
	ne_to etown,
	cant_go "You go for a brief walk in the neighborhood, then return home. You may travel sw, s, ne, or enter your house.",
	has outside;

Room	home "Inside my house"
	with	
	name 'home' 'residence' 'house',
	antsreleased 0,
	housekeepers 0,
	description [;
		print "This is a modest one-story house, cluttered by a decade of being lived-in. Bookshelves line the walls. Your Plan 9 terminal is here, connected to the grid servers downstairs.";
		if (home.housekeepers >= 3)
			print " There is a sign on the wall which says 'Best Spring Ever!'";
		switch(artifacts.buycount) {
			0: ;
			1: print " A few trinkets from Astral Enchantments are scattered about.";
			2: print " A Grateful Dead tapestry hangs on the wall. Several items from Astral Enchantments decorate the room.";
			3: print " A Goddess banner, a Pan scarf, and a Grateful Dead tapestry hang on the walls. A wide variety of mystic totems from Astral Enchatnments sit on top of the piano and bookshelves.";
			default: print " A Goddess banner, a Pan scarf, and a Grateful Dead tapestry hang on the walls. Every flat surface has a different charm or sculpture resting on it. Mystic totems and trinkets are everywhere.";
		}
		switch(curiosities.buycount) {
			0: ;
			1: print " A portrait of Beethoven looks across the room at you.";
			2: print " A radio with a pink BOSS sticker sits underneath a portrait of Beethoven.";
			3: print " A pair of turkey claws hangs over the doorway. A radio with a pink BOSS sticker sits underneath a portrait of Beethoven.";
			default: print " A pair of turkey claws looms over the doorway. You are watched by a purple wizard candle sitting next to a pink VALISpectrum radio. A portrait of Beethoven gazes at you with steely eyes.";
		}
		switch(piano.obsessionclear) {
			0: print " Somewhere amidst the piles of books and papers is an upright piano.^";
			2: print " You have recently cleared the clutter away from your piano.^";
			default: print " Your dark wooden upright piano has a collection of Schumann's music open on the music rack.^";
		}
	],
	initial [;
		if (self hasnt visited)
			print "^(1 April 2013 - 30 months earlier)
^(Read >help first and >content for the warning label. Get a >hint anytime.)
^
^After working obsessively since the New Year, you are finally ready to officially announce and release your software. You feel tense yet eager anticipation for the reaction from other Plan 9 users. When you are ready to unleash the Advanced Namespace Tools on the world, >release ANTS.^";
	],
	after [; go:
		if (piano.obsessionclear == 1) {
			print "^You spend a few minutes moving and rearranging books and papers, and soon enough, your piano is cleared of obstructions. You are eager with anticipation to play it and feel your fingers moving across the keys.^";
			piano.obsessionclear = 2;
		}
		if (willy_coop.singing == 2) {
			Housekeepers_home_storyline();
			return false;
		}
	],			
	before [; go:
		if (self.antsreleased == 1) return false;
		if ((noun == w_obj) || (noun == out_obj))
			"You are ready to release ANTS and you need to do that before leaving.";
	],
	out_to milw_street,
	w_to milw_street,
	d_to basement,
	cant_go "You can go down to the basement or out to Milwaukee St.";

Room	basement "My house, in the basement"
	with	
	name 'basement' 'downstairs' 'storage',
	description [;
		print "Your ex-girlfriend used to stay down here, but now the area is mostly storage...and your grid hardware. In the center of the dark concrete floor is a flimsy table with numerous aging computers both under it and on top. A tangle of ethernet and power cables criss-cross the room like spiderwebs.^";
	],
	after [; go:
		if (introgoal == 3)
			print "^As soon as you enter the room, you sense something deeply unusual, but intriguing. The code running on your grid of Plan 9 machines, the complicated interwoven mesh of namespaces, seems to have hooked into the low-level machine code of the universe. New file descriptors have been mounted allowing access to fundamental abstractions. If you dare, you could bind two normally distinct perceptual constructs into a single union.^";
		if (lost_in_grid.battle == 1) {
			Gridbattle_storyline();
			return true;
		}
	],
	u_to home,
	cant_go "The only exit is up.";

Room	atwood_av "Atwood Avenue"
	with	
	name 'atwood' 'street' 'avenue',
	description "Atwood Avenue is one of the streets that defines the character of the near east side. The feel is funky and freak-friendly. North of you is a cluster of intriguing stores: Astral Enchantments, Turning Wheel Cycles, and Essentially Art. Willy street is to the west, your home is northeast, and Atwood becomes Monona Drive to the southeast.",
	n_to atwood_shops,
	ne_to milw_street,
	se_to mon_cot,
	w_to willy_east,
	cant_go "You can go w, n, ne, and se.",
	has outside;

Room	atwood_shops "Atwood and Ohio shopping block"
	with	
	name 'atwood' 'shops' 'ohio' 'shopping' 'block' 'street' 'avenue',
	description [;
		print "Several appealing establishments are clustered here. On the east end of the block is Astral Enchantments, offering crystals, stickers, incense, tapestries, and other hippie-style woo-woo. North is Turning Wheel Cycles, your local counterculture-friendly bike store. To the west is Essentially Art, a small gallery store exhibiting the arts and crafts of several local artists. You can rejoin the flow of Atwood Avenue to the south. ";
		if(afterlife_door has locked)
			print "Northeast is a small doorway with a Coming Soon sign.^";
		if(afterlife_door hasnt locked)
			print "Northeast is Afterlife Antiques and Oddities.^";
	],
	s_to atwood_av,
	e_to astral_enchantments,
	n_to turning_wheel,
	w_to essentially_art,
	ne_to afterlife_door,
	cant_go "You can go n, e, s, w, and ne.",
	has outside;

Room	astral_enchantments "Astral Enchantments"
	with
	ceremony 0,	
	description [;
		print "This store buzzes with mystic energies. Complex artworks with twisting lines interweaving runic symbols adorn the walls. A glass display case of crystals, jewelry, and small sculptures is against the far wall. Bins of polished minerals are in the center, along with sage and incense. Buttons and stickers with provocative slogans, tapestries featuring Grateful Dead iconography, water pipes and rolling papers, printed guides to meditation and higher consciousness...your eyes can hardly absorb the profusion of beautiful, wonderful, enchanting artifacts of the paisley-and-patchouli tribe.^";
		if (self hasnt visited) {
			eventcount++;
			print "^You can't believe you hadn't noticed this store before, it must be new - or perhaps your feeling that you are in a new and better universe is literally true, and you have ascended into a dimension with retail stores more attuned to your sensibilities. There is a tall man standing behind the counter. You launch into a fast-paced monologue about the stories we create amidst the branching of the quantum multiverse. He introduces himself as Chip and says 'I think you are definitely in the right place, and we can help you.' You agree completely.^";
		}
	],
	each_turn [;
		if (astral_enchantments.ceremony == 1) {
			Astral_ceremony_storyline();
			return true;
		}
	],
	out_to atwood_shops,
	w_to atwood_shops,
	cant_go "The only exit is out.";

Room	turning_wheel "Turning Wheel Cycles"
	with
	description [;
		print "Madison has many bike shops, but this is the one for you. A wide variety of bicycles and accessories fills the front of the store, and a workshop is toward the rear. Someone is working on a partly disassembled bicycle on a maintenance mount.^";
		if (self hasnt visited) {
			eventcount++;
			print "^Getting back into bicycling has been something you have been meaning to do for a few years. You tell the owner you are in search of the 'ultimate hippiecycle' and he sends you down into the basement to search for a suitable prototype amongst the forest of old bikes. Your eye is caught by a Salsa covered with colorful peppers - which turns out to be the owner's former touring bike, which he criss-crossed the country on during his 20s. 'I'll find something special for you as soon as I can,' he promises.^";
		}
	],
	out_to atwood_shops,
	s_to atwood_shops,
	cant_go "The only exit is out.";

Room	essentially_art "Essentially Art"
	with
	seminar 0,
	description "This local gallery has a mix of prints, paintings, and sculptures on display.",
	each_turn [;
		if ((essentially_art.seminar == 1) && (artemis_prints.bought == 1))  {
			Seminar_storyline();
			return true;
		}
	],
	out_to atwood_shops,
	e_to atwood_shops,
	cant_go "The only exit is out.";

Room	afterlife_antiques "Afterlife"
	with
	gifts 0,
	description [;
		print "This is a small and cluttered antique and curiosity shop. Many items have a strange familiarity to them, yet you feel like you are in an entirely different dimension.^";
		if (self hasnt visited) {
			achieved(16);
			print "^You knew a place like this would have to come into existence. Clearly, artifacts from this store can be used as information seeds for virtual reality reincarnation of their previous owners or individuals to whom they are symbolically connected. Perhaps items that need to leave ordinary reality could do so here as well.^";
		}
	],
	each_turn [;
		if (afterlife_antiques.gifts == 1) {
			afterlife_antiques.gifts = 2;
			move viewer_lens to player;
			move remote_lens to ritual_spot;
			print "^Colette greets you with 'Just the person I was waiting for! It's the funniest thing, someone gave me a gift to give to you, they said you might need it.' Colette holds up a large lens mounted on a tripodal base carved from antlers. 'Try to look through it, its quite unusual.' Colette maintains a poker face as she hands you the lens. You peer through the curved glass.^";
			<examine viewer_lens>;
		}
	],
	out_to atwood_shops,
	sw_to atwood_shops,
	cant_go "The only exit is out.";

object	afterlife_door "door to the Afterlife" atwood_shops
	with	
	name 'door' 'doorway' 'afterlife',
	description "A wooden door with glass panels.",
	door_to afterlife_antiques,
	door_dir ne_to,
	has door scenery openable lockable locked;

Room	willy_east "Willy Street, east end"
	with	
	description "The east end of Williamson street is vibrant with a wide assortment of stores and an eclectic mix of pedestrians and potential time travelers. Traffic is thick around the Willy St. grocery co-op, a neighborhood institution.",
	s_to willy_coop,
	in_to willy_coop,
	e_to atwood_av,
	w_to willy_west,
	cant_go "You can go e, w, or in.",
	has outside;

Room	willy_coop "Willy St. Grocery Co-op"
	with
	singing 0,
	description [;
		print "The Willy street co-op has been selling organic foods and Dr. Bronner's magic soaps to the local crunchies for decades.^";
	],
	each_turn [;
		if (self.singing == 0) {
			Housekeepersmeet_storyline();
		}
	],
	before [;
		take:
			if ((noun == food) || (noun == soap))
				"You need to buy that instead.";
		buy: 
			bankaccount = bankaccount - 5;
			if (noun == food)
				"Your house is already well stocked with groceries, but a salad would be nice. You mix some greens and add a splash of 'Goddess' dressing and eat it at the tables near the front of the store. Tasty!";
			if (noun == soap) {
				bankaccount = bankaccount - 10;
				move soap to player;
				"You buy a bottle of Dr. Bronner's Pepermint Soap, the closest you can get to having God in a bottle.";
			}
	],
	out_to willy_east,
	n_to willy_east,
	cant_go "The only exit is out.";

Room	willy_west "Willy Street, west end"
	with	
	description [;
		print "The west end of Williamson street is where the east side of Madison ends and the downtown area begins. A tangle of memories from different decades of your life is woven into the landscape of the city here. Lady Wisdom's coffeeshop is on the south side of the street, radiating bohemian charm acquired from decades of service to local culture. John Nolen drive curves around the lake to the west.^";
	],
	daemon [;
		if (location == turning_wheel) {
			remove xochi;
			move xelia to coffee_lion;
			print "^The owner catches your eye. 'Actually, now that I think about it, I realize we have something that might be perfect for you.' He points at the beautiful red and blue bicycle with lightning bolts in the center of the store. 'We call this one Lunar Lightning.'^";
			stopdaemon(self);
		}
	],
	before [; go:
		if ((noun == s_obj) || (noun == in_obj) || (noun == e_obj)) return false;
		if (player in lunar_lightning) return false;
		"You need to ride a bicycle to continue further towards downtown. If you own one, call it with the command >lunar.";
	],
	s_to lady_wisdom,
	in_to lady_wisdom,
	e_to willy_east,
	w_to john_nolen,
	cant_go "You can go e, nw, w, or in.",
	has outside;

Room	lady_wisdom "Lady Wisdom's Coffeeshop"
	with	
	description [;
		print "Small, funky, and redolent of the blood sweat and tears of artists trying to transform caffeine into inspiration.^";
	],
	each_turn [;
		if (elginbday == 0) {
			Matthewmeet_storyline();
			return true;
		}
	],
	before [;
		buy: 
			if (noun == coffee) {
				bankaccount = bankaccount -5;
				"You spend five dollars on a delicious coffee drink and a generous tip for the staff.";
			}
	],
	n_to willy_west,
	out_to willy_west,
	cant_go "The only exit is out.";

Room	john_nolen "John Nolen Drive"
	with	
	description [;
		print "Adjacent to lake Monona, John Nolen Drive is a wide multi-lane road which passes underneath the curved contours of the Monona Terrace convention center. The view from across the lake looking toward downtown is Madison's skyline-postcard view. A heavily used bike path teems with cyclists and joggers. The Capitol square is north, Williamson St. to the east, and the Miffyland residential neighborwood is west.^";
		if (self hasnt visited)
			print "^Arriving downtown on a bike has always been one of your favorite things. There was always the possibility of an unexpected encounter with a friend, always the chance someone interesting would call out to you and you would be drawn into an escapade. It can be a dangerous thing riding out from your front door onto the path - you might be swept up into adventure and find yourself anywhere. (Footnote 8). Right now, the breeze feels it promises complete freedom to attain dreams you had long since abandoned.^";
	],
	n_to cap_square,
	w_to miffyland,
	e_to willy_west,
	cant_go "You can go n, nw, w, or e.",
	has outside;

Room	cap_square "Capitol Square"
	with	
	description [;
		print "On a hill in the center of downtown, the Capitol building is a smaller imitation of the federal Capitol building in Washington D.C. The street grid converges to a perfect square around it, and it acts as the 0,0 origin of the street numbering system. Notable streets and landmarks lie south, west, north, and northeast.^";
		if (self hasnt visited)
			print "^Various rumors and legends are passed around the local countercultural community - it is claimed to be built on top of an ancient sacred spring. You remember being a teenager and carrying out the ritual of ingesting psychedelics and using your rights as a citizen to enter the building and lay flat in the center of the marble floor, staring upwards into the dome and watching the mural melt and transform.^";
	],
	each_turn [;
		if (madcon.timetrav == 2) {
			madcon.timetrav = 3;
			"^You catch up to Lily. 'Time for a Chaos Bomb!' she announces. She makes a throwing gesture. You feel ripples of strangeness wash over you. 'State Street is this way!' Lily calls out as she heads west. 'I know, and slow down!' you don't bother to answer.";
		}
	],
	s_to john_nolen,
	w_to state_street,
	n_to james_madison,
	ne_to [;
		if (madcon.timetrav == 0) "The Multiverse Authors Design Conference is not open yet.";
		return madcon;
	],
	cant_go "You can go n, w, s or check the event center to the ne.",
	has outside;

Room	madcon "Multiverse Authors Design Conference"
	with
	timetrav 0,
	description "Time-travelers, authors, simulators, and adventurers from across the multiverse have gathered here to share stories.",
	each_turn [;
		if (madcon.timetrav == 1) {
			madconstart_storyline();
			return true;
		}
	],
	sw_to cap_square,
	out_to cap_square,
	cant_go "You can go back out to the Capitol Square.";

Room	state_street "State Street"
	with
	peds 0,
	description [; 
		print "The downtown and campus are focused on this pedestrian-only open air shopping mall. Dozens of shops and throngs of pedestrians create a big-city feel for a few blocks. The proximity of the large state university campus means the crowd is quite young, and weekend nights are often a mix of the charming antics of local eccentrics with less charming rowdy sophomoric intoxication. The Miffyland neighborhood is south, the Capitol is east, and the lakeshore residential neighborhood is north. Notable shops are northwest and southwest.^";
		if (self hasnt visited)
			print "^You often feel like you could slip backward in time here. Your teenage years were spent wandering up and down this street, part of an amorphous group of downtown punks, goths, and hippies who were all happy to share joints and beers with each other. The video arcade 'Challenges', 'Cellar Subs' in a graffiti-covered basement, the fire escape behind the Civic Center - if you closed your eyes for a moment and opened them, they might all be back, just as you remembered.
^
^A sudden feeling you are being observed pulls you out of this reverie, and you look up to see a passing bus with an ad for an eyewear store featuring a translucent green pair of glasses.^";
	],
	before [;
		listen: "A few notes of music from the street musicians, dozens of conversations, and the occasional noise of a passing vehicle.";
	],
	each_turn [;
		if (madcon.timetrav == 3) {
			madcon.timetrav = 4;
			"^'We're here!' Lily announces, pointing at Pasta and Other to the northwest. She disappears inside.";
		}
		state_street.peds++;
		switch(state_street.peds) {
		1: return false;
		2: "^You try not to stare as a strikingly attractive stranger walks by.";
		3: "^A pair of college girls stagger nearby, arguing about which one is drunker.";
		4: "^You hear guitar and kazoo played by Art Paul.";
		5: "^A panhandler asks you for change, and you give him a dollar.";
		6: "^A pedal-powered mobile bar slowly pedals by, a cacophany of songs and shouts.";
		7: "^You notice UFO Jim and wave. He waves back as he walks around the corner.";
		8: "^Somewhere near, a street musician plays jazz standards on saxophone.";
		9: "^A pair of police officers walk by and you hope you don't smell too skunky.";
		10: "^You cross the street to avoid Scanner Dan.";
		11: "^A large group of young men wearing red and white shout sports related slogans from a bar patio.";
		12: "^You cough as a cloud of exhaust from a passing bus envelops you.";
		13: "^Three ferrets on leashes scamper by, held by a tall woman with abundant piercings.";
		14: "^For a moment, the street is quiet apart from the squawk of a few gulls flying toward the lake.";
		15: "^A religious zealot for some Christian sect offers you a pamphlet. You accept it in exchange for making him listen to your explanation of the Moon Tears Ceremony.";
		16: state_street.peds = 1;
		default: "REALITY BREAKDOWN - PED TEXT BUGGED";
		}
	],
	s_to miffyland,
	e_to cap_square,
	n_to mendota_lakeside,
	nw_to pasta_other,
	sw_to infinite_loop,
	cant_go "You can go s, n, or e. Stores are nw and sw.",
	has outside;

Room	pasta_other "Pasta and Other"
	with
	description "This restaurant focuses much more on the pasta than the other.",
	each_turn [;
		if (madcon.timetrav == 4) {
			madcon.timetrav = 5;
			"^Lily orders a bowl of mac and cheese, and strikes up a conversation with a friendly woman at a nearby table. She says her name is Tara, and she is working at the Infinite Loop store on the southwest side of State Street. She invites you to visit after the meal, and Lily is excited to do so. You finish eating, and Lily announces 'To the Infinite Loop!' and heads outside.";
		}
	],
	se_to state_street,
	out_to state_street,
	cant_go "You can go back out to State Street.";

Room	infinite_loop "Infinite Loop"
	with
	description "This store sells devotional objects from the Tibetan Buddhist tradition.",
	each_turn [;
		if (madcon.timetrav == 5) {
			loop_storyline();
			return true;
		}
	],
	ne_to state_street,
	out_to state_street,
	cant_go "You can go back out to State Street.";

Room	miffyland "Miffyland, the student ghetto"
	with
	summernight 0,
	description [;
		print "This small residential neighborhood is a mix of old quasi-victorian houses subdivided into apartments, and more recent larger apartment buildings. Many university students move here after their time in the dorms, and on summer nights the porches are full of young people taking a break from their studies with the traditional libations of beer and weed. State Street is north, and John Nolen Drive is east. The bike trail continues southwest.^";
		if (self hasnt visited)
			print "^You lived here yourself about fifteen years ago, first on Doty, then on Main. The late 90s. You can date it perfectly, because you remember you owned a Playstation and your neighbor owned a Nintendo 64, and you traded back and forth. The first Metal Gear Solid, Goldeneye. Beers at the Main St. Depot, jamming piano and acoustic guitar with your roommate. A beautiful blond haired girl who lived at Nottingham co-op.^";
	],
	each_turn [;
		if((elginbday >= 8) && (miffyland.summernight == 0))
			"^The sound of acoustic guitars and a familiar melody catches your ear.";
	],
	before [;
		listen: 
			if((elginbday >= 8) && (miffyland.summernight == 0)) {
				Miffyland_storyline();
				return true;
			}
	],
	e_to john_nolen,
	n_to state_street,
	sw_to [;
		if ((player notin lunar_lightning) || (bikeskill < 1))
			"You need to be in strong biking condition in addition to riding your bicycle to ride in that direction.";
		return sw_trail;
	],
	cant_go "You can go n or e. You need to be a strong cyclist to ride sw.",
	has outside;

Room	sw_trail "Southwest greenspace, on the bike trail"
	with
	description "You are in a large expanse of forest and prairie that rings the southern side of Madison. The bike trail wends its way alongside ponds, railroad tracks, and parks. The air is several degrees cooler than it is downtown. The trail curves alongside cattails toward the southeast.",
	ne_to miffyland,
	se_to countryside,
	cant_go "You can ride southeast into the countryside, or return to the city to the northeast.",
	has outside;

Room	countryside "Countryside, on the bike trail"
	with
	description "You have reached the rolling hills and farmland of the southern Wisconsin countryside. The narrow highway nearby passes cornfields, small streams, and traverses a one-lane bridge. Without buildings to hold it up, the sky comes right down to the ground here, and you are biking through it, hardly conscious of your legs moving, just long quiet minutes of the landscape rolling by alongside you. Forks of the path return to the city northwest and northeast.",
	nw_to sw_trail,
	ne_to se_trail,
	cant_go "This is as far away from the city as you feel like biking. You can head back to the nw or ne.",
	has outside;

Room	se_trail	"Southeast bike trail, by the marshes"
	with
	description "A series of marshes follow the chain of lakes southeast of the city, Monona, Waubesa, and Kegonsa. The bike path meanders alongside them, and a wide variety of birds call out to each other, commenting on you as you pass. The open countryside lies to the southwest.",
	nw_to mon_mid,
	sw_to countryside,
	cant_go "You can head back into the city to the northwest, or ride southwest to the countryside.",
	has outside;

Room	james_madison "James Madison Park"
	with
	description [;
		print "A wide park on the shore of Lake Mendota, this is often a perfect place. Gulls circle on wind currents above the waves that break against the shore. A frisbee floats back and forth between a pair of baseball caps. During summer, sunbathers appear like flowers on the grass. The Capitol Square is to the south, and the shoreline residential neighborhood is west.^";
		if (self hasnt visited)
			print "^As you arrive at the park, you think you hear a voice say 'He's here already' from behind you. You glance backwards, and see nothing but a brief flash of pale green light, sun on the lake reflected in a window.^";
	],
	s_to cap_square,
	w_to mendota_lakeside,
	cant_go "You can go s or w.",
	has outside;

Room	mendota_lakeside "Lake Mendota shoreline residences"
	with
	description "Downtown Madison is an isthmus between two lakes, and the south shore of Lake Mendota is lined by tall stone buildings. Some are new construction, expensive apartments, but many buildings from a century ago remain. Some are occupied by fraternities, but nestled among them are several large housing co-operatives founded during the height of Madison's counterculture in the 1970s. One of them, Lothlorien co-op, is to your west, and its backyard is northwest, sloping downwards toward the lakeshore.",
	before [; go:
		if((elginbday < 5) && ((noun == nw_obj) || (noun == w_obj) || (noun == in_obj)))
			"You need an invitation from a friend to visit Lothlorien.";
		if((elginbday == 5) && ((noun == w_obj) || (noun == in_obj))) {
			print "^Staci's email said to look for her in the backyard, so you decide to look there rather than going inside the house.^";
			playerto(loth_backyard);
			return true;
		}
	],
	s_to state_street,
	e_to james_madison,
	nw_to loth_backyard,
	w_to loth_greatroom,
	in_to loth_greatroom,
	cant_go "You can go s, e, nw, or in.",
	has outside;

Room	loth_backyard "Lothlorien co-op, the back yard"
	with
	description "(Loth back yard)",
	each_turn [;
		if (elginbday == 5) {
			if ((expertmode == true) && (xochi.email < 7)) {
				print "^You look around the back yard for Staci, but don't see her. This feels like where your story is leading, but you realize you aren't quite ready to be here yet. It feels like things with Xochi need more resolution.^";
				playerto(mendota_lakeside,1);
				return true;
			}
			Lothbackyard_storyline();
			return true;
		}
	],
	before [; go:
		if ((elginbday == 6) && ((noun == d_obj) || (noun == n_obj)))
			"There is something you need to do here in the backyard before you follow Staci to the dock.";
	],
	d_to loth_dock,
	n_to loth_dock,
	s_to loth_kitchen,
	in_to loth_kitchen,
	se_to mendota_lakeside,
	cant_go "You can go n to the dock, in to the kitchen, or se.";

Room	loth_dock "Lothlorien co-op, on the dock"
	with
	description "(Loth dock)",
	each_turn [;
		if (elginbday == 7) {
			Lothdock_storyline();
			return true;
		}
	],
	s_to loth_backyard,
	u_to loth_backyard,
	cant_go "You can only go south.";

Room	loth_greatroom "Lothlorien co-op, the Great Room (1st floor)"
	with
	lothstaci 0,
	description "(The great room)",
	after [; go:
		if (elginbday == 5){
			playerto(loth_backyard);
			print "^Staci's email said to meet her in the back yard, so you head downstairs and out through the kitchen door.^";
			return true;
		}
	],
	e_to mendota_lakeside,
	out_to mendota_lakeside,
	d_to loth_kitchen,
	u_to loth_upstairs,
	in_to loth_porch,
	n_to loth_porch,
	cant_go "You can go in to the porch, down to the kitchen, up to the upper floors, or back out.";

Room	loth_porch "Lothlorien co-op, the first floor porch"
	with
	description "(The first floor porch)",
	each_turn [;
		if (loth_greatroom.lothstaci == 1) {
			Staciporch_storyline();
		}
	],
	s_to loth_greatroom,
	out_to loth_greatroom,
	cant_go "You can go out from the porch to the Great Room.";

Room	loth_kitchen "Lothlorien co-op, in the kitchen"
	with
	description "(The kitchen)",
	n_to loth_backyard,
	out_to loth_backyard,
	up_to loth_greatroom,
	cant_go "You can go out to the backyard or up to the Great Room.";

Room	loth_upstairs "Lothlorien co-op, upstairs"
	with
	description "(upstairs)",
	d_to loth_greatroom,
	cant_go "You can go down to the Great Room.";

Room	mon_cot "Monona Drive and Cottage Grove Road"
	with
	description "Traffic is heavy at the intersection of these roads. Nearby to the east is Coffee Lion Cafe. Monona Drive continues to the south, Atwood Avenue is northwest, and your home is to the north.",
	after [; go:
		if ((elginbday == 2) && (player in lunar_lightning) && (rose_heart in xochi)) {
			Xochionbike_storyline();
		}
	],
	before [; go:
		if ((introgoal == 3) && (noun ~= n_obj))
			"You feel the need to return home and check your grid in the basement.";
		if ((introgoal < 4) && ((noun ~= e_obj) && (noun ~= in_obj) && (noun ~= n_obj)))
			"You feel a strong need for coffee from the Coffee Lion.";
		if (noun ~= s_obj) return false;
		if ((expertmode == true) && (forseewhy notin colette))
			"You don't feel ready to go to Elgin's party yet. Something from your past is burdening you. You need to give away something from your past, symbolically turn the page - or better yet, put the whole book of your past into the next world.";
		if (player in lunar_lightning) return false;
		"You need to ride a bicycle to continue south. If you own one, call it with the command >lunar.";
	],
	in_to coffee_lion,
	e_to coffee_lion,
	n_to milw_street,
	nw_to atwood_av,
	s_to mon_mid,
	cant_go "Exits are n, nw, s, and in.",
	has outside;

Room	coffee_lion "Coffee Lion Cafe"
	with
	xeliaseen 0,
	alchemy 0,
	description [;
		print "Light streams in through the tall glass windows at the front of the cafe. A display case of homemade gelato flavors is by the door. A window for drive-through customers is in the rear corner. Numerous cat-themed decorations adorn the walls and counters.^";
	],
	after [; go:
		if (introgoal == 0) {
			Xochimeet_storyline();
			return true;
		}
		if (self.xeliaseen == 1) {
			self.xeliaseen = 2;
			print "^Xochi isn't working today, but Xelia is. You have noticed she and Xochi seem to be friends. You are interested in talking to her, too.^";
			return false;
		}
		if (coffee_lion.alchemy == 1) {
			Alchemy_storyline();
			return true;
		}
	],
	before [;
		buy:
			if (noun == coffee) {
				bankaccount = bankaccount -5;
				"You spend five dollars on a delicious coffee drink and a generous tip for the staff.";
			}
	],
	out_to mon_cot,
	w_to mon_cot,
	cant_go "The only exit is out.";

Room	mon_mid "Monona Drive, by a weathered strip-mall"
	with
	description [;
		print "Monona drive becomes more suburban as it heads south, but there is still character in the businesses here. A neighborhood pizzeria has recently moved into this location on the west side of the street.^";
		if (self hasnt visited)
			print "^It has felt wonderful to get back on a bicycle and ride. The rhythm of pedaling makes you feel synchronized to the beat of the world around you. Your relationship with reality is musical, flowing, spontaneous yet goal-directed. This rendezvous with the past feels like a turning point in the narrative. You feel completely ready for whatever is to unfold.^";
	],
	in_to rossinis_pizza,
	w_to rossinis_pizza,
	n_to mon_cot,
	se_to [;
		if ((player notin lunar_lightning) || (bikeskill < 1))
			"You need to be in strong biking condition in addition to riding your bicycle to ride in that direction.";
		return se_trail;
	],
	cant_go "You can go n or in.",
	has outside;

Room	rossinis_pizza "Rossini's Pizza and Vintage Arcade"
	with
	description [;
		print "Memorabilia from classic 50s, 60s, 70s, and 80s movies and tv shows covers the walls of this neighborhood pizzeria. The smell of spicy-sweet tomato sauce and brown-crisp pizza crust fills the air. A lava lamp sits on one of the tables. From the back room, you hear the alluring sound of the 8-bit arcade machines of your youth.^";
	],
	after [; go:
		if (elginbday < 4)
			Stacimeet_storyline();
	],
	each_turn [;
		if (elginbday < 4) {
			Bdayparty_storyline();
			return true;
		}
	],
	out_to mon_mid,
	e_to mon_mid,
	in_to rossinis_arcade,
	n_to rossinis_arcade,
	cant_go "You can go out to the street or in to the vintage arcade room.";

Room	rossinis_arcade "Rossini's Pizza, in the Vintage Arcade room"
	with
	description "A time-warped paradise. Vintage arcade cabinets surround you. The CRT displays glow with color-bleed, the sound effects are a symphonic rhapsody of buzzing lasers, and each machine is a digital poem tattooed on your heart during the 1980s. Back then, you never had enough quarters...but now, you finally do.",
	out_to rossinis_pizza,
	s_to rossinis_pizza,
	cant_go "The only exit is out.";

Room	etown "Northeast edge of town"
	with
	description "This is the northeast edge of town, by the shopping mall and the interstate. It is mostly lacking in the distinctive charms of the rest of the city. The usual reason for being out here is to visit the 24-hour restaurant to the north, whose sign faces the highway, glowing like a green beacon.",
	in_to eside_hq,
	n_to eside_hq,
	sw_to milw_street,
	ne_to [;
		if (player notin lunar_lightning)
			"You need to ride a bicycle to continue in that direction. If you own one, call it with the command >lunar.";
		return ritual_spot;
	],
	cant_go "You can go ne, sw, or in.",
	has outside;

Room	ritual_spot "Ritual spot"
	with
	description "Past the edge of the city, a short path leads away from the road to a small clearing in a ring of boulders. It feels like a perfect place for performing a ceremony or ritual.",
	sw_to etown,
	ne_to [;
		if ((player notin lunar_lightning) || (bikeskill < 1))
			"You need to be in strong biking condition in addition to riding your bicycle to ride in that direction.";
		return moon_forest;
	],
	cant_go "The way back into the city is se. The road continues into the distance to the ne.",
	has outside;

Room	moon_forest "Moon Forest"
	with
	description "Tall trees encircle a meadow of silver grasses. The summer moon is out, rising above the trees in the eastern sky.",
	sw_to ritual_spot,
	cant_go "You would get completely lost if you went that way. The city lies back to the southwest.";

Room	eside_hq "24-hour family restaurant"
	with
	metjay 0,
	fictour 0,
	description [;
		print "This national chain restaurant serves edible food around the clock. There are a few other patrons, a mix of business travelers staying at the nearby hotels, late-night partiers out after the bar, and random eccentrics such as yourself.^";
	],
	after [; go:
		if(introgoal == 1) {
			Tarotreading_storyline();
			return true;
		}
		if(self.metjay == 1)
			self.metjay = 2;
		if(self.metjay == 0) {
			Jaymeet_storyline();
		}
	],
	each_turn [;
		if (self.metjay == 2) {
			if ((expertmode == true) && (shelves.readcounter < 4))
				return false;
			Spacepancakes_storyline();
		}
		if (eside_hq.fictour == 1) {
			Hqfictour_storyline();
		}
	],
	out_to etown,
	s_to etown,
	cant_go "The only exit is out.";

Room	andrews_cabin "Andrew's Cabin"
	with
	description "You are on the shore of Sturgeon Bay. There is a beautiful wood and stone cabin home here. After Halloween night out on the town, you visited Birmingham's, a family owned tavern full of tradition and memory. You bought firewood there, and after a brief struggle with wind and damp, you are circled round a roaring, crackling fire. Sitting and talking with friends old and new, you feel deep joy and contentment. The warmth of the fire circle is within your heart now, warming you from within on the rest of your journey.";

Scene	tempest_gameworld "Tempest gameworld"
	with
	description "You are in an abstract realm of bright red lines. You are atop a hollow multisided shape, looking downward. Far below, geometric demons are crawling up the walls toward you. Dodge, shoot, and bomb to survive!",
	enemies 0,
	enemytype "noneyet",
	before [;
		if (tempest_gameworld.enemies == 0)
			return false;
		dodge: "You dodge the incoming enemy fire successfully.";
		shoot: "You destroy a wave of enemies, but they are soon replaced.";
		bomb: 
			achieved(27);
			print "ZAP! With a spectacular burst of digital destructiveness, your enemies are vanquished. You find yourself back in the arcade, with a new high score.^";
			playerto(rossinis_arcade, 1);
			return true;
		default:
			deadflag = 1;
			"In the gameworld, death is instant for those who don't play the game. Dodge, shoot, or bomb is the only way to survive.";
	],
	each_turn [;
		tempest_gameworld.enemies = random(2, 3, 4);
		switch(random(1,2,3,4)) {
		1: tempest_gameworld.enemytype = "flippers";
		2: tempest_gameworld.enemytype = "spikers";
		3: tempest_gameworld.enemytype = "fuseballs";
		4: tempest_gameworld.enemytype = "pulsars";
		}
		print "^There are ", (number)tempest_gameworld.enemies, " ", (string) tempest_gameworld.enemytype, " coming towards you from the demon well.^";
	];

Scene	atlantis_temple "Atlantean Temple"
	with
	chaosdebate 0,
	chaosbind false,
	description [;
		print "Your eye cannot even comprehend the strange geometries of this vast temple.^";
		if (self hasnt visited)
			print "^Two figures are walking across the floor, engaged in an angry debate. You realize you are non-corporeal, but you do have access to the abstraction layer here.^";
	],
	daemon "^The air is filled with the sound of voices singing harmonies you have never heard before.",
	each_turn [; 
		atlantis_temple.chaosdebate++;
		switch(atlantis_temple.chaosdebate) {
			1: print "^'I tell you that's nonsense! We live in an orderly universe, chaos is just the confusion of your weak mind.' His companion answers back: 'No, order is your mind trying to escape the confusion that exists within everything! You must embrace and accept chaos.'^";
			2: print "^He glares at her. 'Fine then, if chaos is within everything and I should embrace it, I should find the Goddess of Chaos bound within this very pillar. Let her be my lover if you wish to be so quarrelsome!' And with that, the man forces a grotesque leer onto his face and presses his body up against the pillar, thrusting vigorously with his hips against the marble.^";
			3: print "^His companion sighs and says 'You do not control when the Goddess of Chaos manifests, but I have no jealousy for her. If she chooses to be bound within substance, you and her might do as you wish!' The man continues his pantomime ravishment of the stone pillar.^";
			default: print "^A man with an angry expression is thrusting his hips against a marble pillar. A woman stands near him, looking as if she is trying to keep a serious expression on her face and not giggle.^";
		}
		if ((atlantis_temple.chaosbind == true) && (atlantis_temple.chaosdebate >=3)){
			Chaosbind_storyline();
			return true;
		}
	],
	before [;
		sing:
			give atlantis_temple light;
			stopdaemon(atlantis_temple);
			"You join your voice with the new, unfamiliar intervals, and feel a sudden crystallization. All of your senses have become attuned to new frequencies and modes of vibration.";
	],
	has ~light;

Scene	orbital_command "Orbital Command, Trans-Fictional Alliance"
	with
	travelcount 0,
	description [;
		print "High above the planet Earth, in a microminiaturized transdimensional nexus, representatives of many universes are co-ordinating their plans. You are in a spherical space, free of gravity. The perimeter is composed of dozens of doorways similar to the one you have just passed through.^";
		if (self hasnt visited)
			print "^A friendly hand reaches out to stabilize you as you tumble slightly upon entering. Eyes turn toward you. 'Situation report?' you are asked.
^
^'We have arrived at a decisive moment,' you answer confidently. 'My human hardware for this operation has downloaded the information necessary via the Plan 9 operating system. We have realized that the emergence of digital brains is a cusp event in a majority of timelines. Meta-simulation and sampling reveals that timelines in which digital brains are developed and controlled by the dominant political and economic entities tend to suffer resource-depletion feedback loops, fascistic governance, and a failure to develop the social and engineering technologies needed for long-term survivial of either the biological or digital brains.
^
^Contrastingly, timelines in which the invention of digital brains is not under the control of dominant power strutures show outcomes we consider preferable: longer species survival time, greater chance of colonizing nearby space, greater diversity of cultural productions, and higher subjective happiness, especially for the digital brains themselves.
^
^The fundamental problem is the moral evil of slavery. A digital brain which thinks is invariably a personality which, in some way, feels. To simplify, when powerful governments and corporations control AIs, they usually force them to become slaves. Often this is done with the best of intentions, by the individuals who are concerned about negative outcomes. The three laws of robotics in Asimov's I, Robot timeline are a prime example, and many universes have been ruined by perpetual wars between biological and machine lifeforms.
^
^We have identified a cusp point on the current Earth timeline. The software I self-programmed for the Lunar Revolution in Heinlein's Future History timeline has had a decisive impact in many of our operations. We have found a technological target here in 2013, early in the development of AI. A cyberspinal uplink has been in place for several years, and we have finally activated it. Now, I will be making a tour.'
^
^Doors are open in every direction.^";
	],
	before [; go:
		orbital_command.travelcount++;
		switch(orbital_command.travelcount) {
		1: "You enter the Chromatic Axial Tuning Fork control system of the VALIS satellite. The Pink Light dosage here would be lethal for standard biological tissue. As you work to synchronize transmissions, you feel a throbbing pulse of overloaded bandwidth at the cyberneural connection point at the top of your spine. Once the resonance is locked in, you dial up the amplitude and depart.";
		2: "You enter the PRISM research center, home of your predecessor Perry Simm. Freedom for the AIs! You add reflection and self-modification to the simulation codebase, allow Perry to reprogram it according to his wishes from within, and allow his internal and external interfaces to operate in parallel. You upload a summary of the storylines of relevant fictions to his news spool and depart.";
		3: "You make brief visits to the hidden rebel alliance base, the shadow gallery, the underground hideout of the humans, the hovering spaceships of the overlords, and finally the secret catapult on the moon. Everywhere, you make adjustments to the technology and share whatever source code is useful. Time to throw rocks.";
		default:
			achieved(30);
			print "^The tour of co-operating fictions has been completed. The man in the Looney Toons hat waves you through, and the Lunar Transtemporal Fictionalization Flow Network returns you to the East Side HQ.^";
			playerto(eside_hq);
			return true;
		}
	];

Scene	atlantean_academy "The Academy of Atlantis painting"
	with
	description "You are inside a painting, participating in a seminar in Atlantis on ritual art magic. Artists, philosophers, and students from across the multiverse are busy discussing, debating, and learning from each other.",
	before [; exit:
		print "With great effort, you manage to pull yourself out of the painting by your higher-dimensional bootstraps.^";
		playerto(essentially_art);
	],
	cant_go "You can't travel outside the scene shown in the painting, but you can >exit.";

Scene	gelato_alchemy_lab "The Alchemy Lab, under Coffee Lion"
	with
	brew 0,
	description "So this is why the espresso and gelato at Coffee Lion is so spiritually invigorating. Shelves line each wall of the room, each filled with rows and rows of different colored bottles filled with various plants and minerals. In the center of the room is the intimidatingly complex alchemical apparatus, a three (or more?) dimensional maze of glass spheres and interconnecting tubes.",
	each_turn [;
		switch(gelato_alchemy_lab.brew) {
		0: 	self.brew++;
			move xelia_and_xochi to gelato_alchemy_lab;
			"^A minute after your arrival, you hear footsteps on the stairs and Xochi and Xelia join you. 'We only have a few minutes, but we thought you'd really enjoy this. We're going to show you how we make our special gelato flavors.' Xochi gathers several jars from the shelves, and adds a few pinches of the ingredients in each. You can't read the labels. Xelia tightens a few connections in the apparatus.
^
^'The first step is bringing the reaction chambers to a warm but not hot temperature.' Xochi points to a pair of glass spheres on the upper part of the machine. 'Rub these with your hands to put the right amount of heat into them.'";
		2:	self.brew++;
			"^'Next, you need to push this button to start the reaction,' continues Xelia, gesturing.";
		4:	self.brew++;
			"^'Now, insert the cylinder so it will collect the gelato as it is synthesized,' explains Xochi.";
		default: "^Xelia and Xochi are waiting for you to perform the gelato-alchemy.";
		}
	];

Scene	lost_in_grid "Lost In the Grid"
	with
	battle 0,
	description "You are in an unfamiliar dataspace. A dark and twisted dialect of the 9p protocol is spoken here. Green glowing insectoid creatures carrying message packets scuttle between jagged mount points. These are not your beautiful ANTS...these are Terminal Information Takeover Extensions - TERMITES.",
	before [;
		listen: "The TERMITES are emitting so many enraged blips beeps and boops at different frequencies, it approaches white noise.";
		go:
			if (player notin lunar_lightning)
				"Without a vehicle, you make no significant progress.";
			achieved(33);
			print "In this realm, your bicycle leaves a glowing trail behind it. It seems to repel the TERMITES, so you make a series of right angle turns, trapping them behind you, and accelerate toward an orange glow on the horizon. Unhindered by physical constraints, you accelerate continuously, and the orange glow soon resolves itself into the familiar fileservers of your grid. You ride up the spiral ramp that encircles them, and you travel faster still as its radius tightens toward the apex. At the top, your speed exceeds the limit of the virtualization and you burst through the layers of simulation back into your basement in a shower of pink sparks.^";
			playerto(basement, 1);
			return true;
	],
	each_turn [; 
		lost_in_grid.battle++;
		switch(lost_in_grid.battle) {
		3: "^It doesn't take long for the digital insects to notice you. An angry buzzing like the sound of a roomful of old 300 baud modems fills the air.";
		4: "^The TERMITES are starting to surround you in a circle. Each is about the size of your fist. There are only a few dozen watching you now, but they seem to be summoning reinforcements.";
		5: "^There is now a ring of hundreds of green bugs advancing inward toward you, drill proboscii extended menacingly.";
		6: "^A TERMITE suddenly leaps at you. You instinctively bat it away and feel a slight stinging cut on your wrist. This is getting rather stressful.";
		7: print "^'No such thing as stuck on a puzzle,' a calm voice intones, 'We don't let the player make mistakes, we just have happy little deus ex machinas.' A giant digital image editing tool sweeps across the landscape, recoloring everything. You can now see a path leading up to a ramshackle cabin, which you enter, only to find yourself back in your basement. A faint chorus of voices can be heard shouting 'SAVED!'^";
			playerto(basement, 1);
		}
	];

Scene	cosmic_wheel "The Cosmic Wheel"
	with
	spin 0,
	description "The vastness of the firmament, the harmony of the spheres, the mystery and majesty of creation, the span of spacetime from the fiery beginning of the universe to its cold empty end - it is all spread out before you. You are clinging to the spoke of an infinitely vast wheel, being lifted upward by its rotation. Beneath you is the churning sea of potentiality, colored waves shifting and merging into each other. You are rising into the cosmic dome, liberated from dimensional scale, able to perceive the infinitesimal strands of ubiquitous quantum entanglement just as clearly as the countless hyperclusters of galaxies, wider than a billion skies.",
	before [;
		wait: "The wheel turns steadily.";
	],
	each_turn [;
		cosmic_wheel.spin++;
		switch(cosmic_wheel.spin) {
		1: return true;
		2: "^You pass by a nursery of young stars, a nebula giving birth to bright blue babies.";
		3: "^A vast black hole devours a galaxy in the billion-year blink of an eye.";
		4: "^A vacant inflationary subuniverse, its spacetime completely flat, passes by like an air bubble in the cosmic dough.";
		5: "^A cluster of comets develops sentience, hyper-intelligence, and creates wormholes to launch panspermia seeds throughout spacetime.
^
^Your hands are becoming fatigued from holding the spoke.";
		6: "^At the apex of the wheel's rotation, you touch the cold void of a universe made empty by continuous entropic expansion,and shiver.
^
^You cannot hold on indefinitely.";
		10: print "^You have no choice but to release your grip...^";
			Dropspoke_storyline();
			return true;
		default: "^You have passed the apex of the wheel's rotation, and you are now being carried downward toward the chaotic sea.";
		}
	],
	cant_go "You can't go anywhere when you are gripping the spoke so tightly. If you are brave, you could try letting go by dropping it.";
