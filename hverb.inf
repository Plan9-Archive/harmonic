!============================================================================
! Entry point functions

[ Amusing;
	print "Have you tried...
^
^ATTACK Strawberry?
^TAKE fiction?
^READ books over and over?
^REVIEW someone's opinion on the game?
^BUY all the ARTIFACTS in Astral Enchantments and ANTIQUES in Afterlife, and then LOOK inside your home?
^TELL Xelia about Xochi?";
];

[ Deathmessage;
	switch(deadflag) {
	3: print "You have violated simulation parameters. Please UNDO.";
	default: "This branch of the game universe has come to an end.";
	}
];

[ Timepasses;
	if (elginbday == 1) {
		if((eventcount >= 3) && (location has outside)) {
			if ((expertmode == true) && (turning_wheel has visited))
				Bdayinvite_storyline();
			if (expertmode == false)
				Bdayinvite_storyline();
		}
	}
	if (clockoff == true)
		return true;
	if (the_time == 0){
		daynumber = daynumber + 1;
		print "^Midnight strikes and a new day dawns.^(This is day number ", daynumber,")^";
		switch(daynumber){
		1: if (home.antsreleased == 0)
			Antsrelease_storyline();
		3: if (introgoal == 0) {
			print "(You travel to Coffee Lion)^";
			Xochimeet_storyline();
			}
		4: if (introgoal == 1) {
			print "(You travel to the late-night restaurant)^";
			Tarotreading_storyline();
			}
		6: if (introgoal == 2) {
			print "(You travel to Coffee Lion)^";
			coffee_lion.xeliaseen = 2;
			Xeliawardrobe_storyline();
			}
		8: if (introgoal == 3) {
			print "(You find Fiction and Reality linked to your grid, and you bind them together)^";
			Ficbind_storyline();
			}
		20: if (elginbday == 0) {
			print "(You travel to Lady Wisdom's coffeeshop)^";
			Matthewmeet_storyline();
			Bdayinvite_storyline();
			lunar_lightning.bought = 1;
			}
		28: if (elginbday < 4) {
			print "(You go to Elgin's birthday at Rossini's Pizza and Arcade)^^";
			Stacimeet_storyline();
			Bdayparty_storyline();
			Bday_term_storyline();
			}
		36: if (elginbday < 6) {
			print "(You go to Lothlorien co-op to see Staci)^";
			Lothbackyard_storyline();
			print "^(You see future and past here, and bind them together)^";
			Futurebind_storyline();
			Lothdock_storyline();
			}
		42: if (elginbday < 10) {
			Bday_visit_storyline();
			Firstlothvisit_storyline();
			}
		default: return true;
		}
	}
];

[ Initialise;
	lookmode = 1;
	location = home;
	player.description = "You are 38 years old, long-haired and bearded, slightly disabled from rheumatoid arthritis. You are slightly fat but have been losing weight lately from a fritos-only programmer's diet.";
	settime(0,60);
	move wallet to player;
	move mirror_shades to player;
	print "^^The full moon amidst clouds spills milky brightness everywhere. Summer is just barely over but the air is still warm and moist. Staci is standing on tip-toe, looking over the edge of the rooftops and treebranches. 'Oh! I can tell it's started,' she says. You stand up and walk across the porch, slipping your arm around her as you turn to watch the sky. What had been a low full circle wreathed by clouds is higher now and shinining clearly, but no longer full - darkness is creeping across the surface as the Moon swings into Earth's shadow. You peer upwards together, savoring the moment. The night air is sweet and warm, you are with someone you love...and the Castle is about to reopen.^^";
	getenter();
];

[ PrintTaskName task_number;
	switch (task_number) {
	0: "releasing ANTS";
	1: "meeting Xochitl";
	2: "binding Fiction onto Reality";
	3: "playing your piano after cleaning it";
	4: "composing 'Orange Night Light'";
	5: "composing 'I heard the song first at fifteen'";
	6: "composing 'Get out on the dock'";
	7: "composing 'Always will remember July'";
	8: "composing 'Last Full Moon'";
	9: "composing 'Moon Computer'";
	10: "composing 'Don't Tell Me What Year'";
	11: "meeting Jay";
	12: "summoning the Singing Hippie Housekeepers";
	13: "sending email to Xochi and hearing 'Heartbeat'";
	14: "giving the rose quartz heart to Xochi";
	15: "being invited to Lothlorien co-op by Staci";
	16: "entering Afterlife";
	17: "giving up 4CY to the Afterlife";
	18: "meeting Xochi on bike on the way to Elgin's birthday party";
	19: "making it to Loth dock with Staci in May";
	20: "joining the miffyland rooftop singalong";
	21: "becoming the Harmonist";
	22: "wearing the Loonie Revolution Power Pendant";
	23: "playing Tempest";
	24: "performing the ritual for Hecate";
	25: "telling Xochi she is the reincarnation of Clara Schumann";
	26: "ending movement no. 1 of the symphony";
	27: "escaping from within the Tempest gameworld";
	28: "rapping to Xelia about Xochi";
	29: "binding Chaos onto Substance in Atlantis";
	30: "touring related Fictions from the Orbital nexus";
	31: "helping Atlantean students with a test on your work";
	32: "brewing a special affogato";
	33: "escaping from the TERMITES on lunar lightning";
	34: "letting go of the Cosmic Wheel spoke";
	35: "binding Fortune onto Story";
	36: "participating in Madcon";
	37: "smoking in the woods on 19 June";
	38: "biking around the lake on the Last Full Moon";
	39: "completing the main historical storyline";
	40: "exploring the Jennifer-Spaight neighborhood on bike";
	41: "following rabbits to discover new bike paths";
	42: "joining Staci in her room at the co-op";
	43: "performing with Staci against corporate personhood";
	44: "biking to the cascade aerator";
	45: "following Glenda out of the street maze";
	46: "harmonizing the beams inside the moon";
	47: "breaking the green screen";
	48: "putting out the Loth fire";
	49: "telling Xelia about your summer";
	}
];

!============================================================================
! Utility functions

[ Getenter;
	print "^Press enter to continue.^^";
	keycharprimitive();
];

!============================================================================
! Verb functions

[ Aboutsub;
	"^Harmonic Time-Bind Ritual Symphony is a game based on my spring and summer during 2013. During that time, I was able to experience my life as a work of interactive fiction created by a brilliant author. I felt that the nature of the quantum multiverse meant that it was true that I was a character in novels and songs and videogames in other universes, and I played my role to the hilt. I believed my manic madness was a gift which I could use for artistic creativity, and that my racing mind was fully up to solving the puzzles that the designer of my reality had laid out for me. It was the most glorious experience imaginable, and by the middle of the summer I felt like I was becoming a kind of Ultima IV-style Avatar of multiversal artistic consciousness and with just a little more spiritual insight and creative freedom, I'd be able to unlock the Vishnu-path Extra Arms upgrade for my character, which I might need in case the tentacle in the lake that was a whisker of Cthulhu's Moustache attacked.
^
^This game was created to share the wild, unbounded joy I felt in those months. Thanks to everyone who was a part of my life during that time, and thank you for playing this work of Interactive Fiction. Read >help for important tips. >design prints a spoiler-free essay on the design of the game.^";
];

[ Bindsub;
	if((introgoal == 3) && (((noun == fiction) && (second == reality)) || ((noun == reality) && (second == fiction)))) {
		Ficbind_storyline();
		return true;
	}
	if((elginbday == 6) && (((noun == past) && (second == future)) || ((noun == future) && (second == past)))) {
		Futurebind_storyline();
		return true;
	}
	if((xochi.email == 3) && (((noun == schumann) && (second == player)) || ((noun == player) && (second == schumann)))) {
		Schumannbind_storyline();
		return true;
	}
	if(((noun == chaos) && (second == substance)) || ((noun == substance) && (second == chaos))) {
		atlantis_temple.chaosbind = true;
		print "^You make the bind, and the Goddess of Chaos winks at you as she merges herself with the material substance of reality. She's been there all the time, of course.^";
		return true;
	}
	if(((noun == storybind) && (second == fortune)) || ((noun == fortune) && (second == storybind))) {
		fortune.dodge_fate = true;
		achieved(35);
		print "^The lens allows you to make the bind, and although there is no overt effect, you feel much less worried about potential plot twists. Things have been so magical this summer, and the feeling that everything is too good to be true, too perfect to last, has sometimes given you a vague unease. That feeling is gone now.^";
		return true;
	}
	"^You attempt the bind of ", (name) noun, " on top of ", (name) second, " but nothing interesting happens...";
];

[ Bombsub;
	"Not a good idea unless you are inside a videogame.";
];

[ Clockonsub;
	clockoff = false;
	"Clock story triggers set to on. >sleep advances to the next day, >story advances to the next major plot event.";
];

[ Clockoffsub;
	clockoff = true;
	"Clock story triggers set to off. No plot events will occur automatically. >clockon to reactivate.";
];

[ Commentarysub;
	if (commentary == false) {
		print "Turning commentary on, enter >commentary again to deactivate.^";
		commentary = true;
		return true;
	}
	print "Turning commentary off.^";
		commentary = false;
];

[ Contentsub;
	"^This game contains scenes of drug use, sexual behavior, classical music, parody of religion, embrace of the counterculture, revolutionary propaganda, true love, use of nonstandard computer software, middle-aged men flirting with young baristas, and bicycling without a helmet. During the game, your character may do these things regardless of your inputs, and enjoy them very much. If you object to these behaviors, or don't enjoy games with a lot of cutscenes where the character makes choices outside your control, you may wish to play something else. I consider this game suitable for ages 13+ but please evaluate the content yourself rather than relying on my judgment. I may be a Loonie.";
];

[ Creditssub;
	"^Written and programmed by Ben Kidwell (mycroftiv)
	 ^Co-written and edited and tested by Maevele Straw^
	 ^Thanks to Inform Beginner's Guide and everyone maintaining Inform 6 resources^";
];

[ Dancesub;
	"Despite your arthritis, you enjoy trying to put the rhythms of music inside your body. You dance in a loose, disorganized fashion, but it feels good.";
];

[ Designsub;
	"This is a story-focused game where most of the action takes place in cutscenes. This approach is sometimes criticized for lack of interactivity and denying player agency. This essay tries to explain the choices made in the overall game design, role of puzzles, and the time and story system.
^
^IF is an incredibly flexible medium. No single standard of evaluation is adequate because a wide range of preferences exist among players. Emphasis on puzzles vs. emphasis on narrative is the most significant. Another is between 'the game is a story about ME' and 'the game is a story where I take on the role of a different personality'. Other topics with diverse opinion include preferred game length, the importance of player agency and diverging story branches and multiple endings, menu-based vs traditional character conversation, and what level of world-modeling is expected.
^
^Puzzle-focused games with a lot of player agency to cause diverging story branches are wonderful, but this game is not one of them. The artistic intention of this game is to make the player feel what it is like to be on a manic upswing where everything in life actually works out just the way you dream. The reason it is IF rather than a novel is twofold: first, I believe IF is a uniquely compelling medium for transmitting what it felt like to experience something, and second, I actually did perceive myself as a character in a traditional IF game. I bought the items I collected from the stores in the game becuase I actually believed I would need them to solve future puzzles. I could practically see the prompt at which my actions were being typed.
^
^As for puzzles - I love logic puzzles and strategy games and cleverness of all kinds, but when I'm emotionally involved in a story, I want to keep experiencing it and not get stuck. The subjective experience of being stuck is the opposite of everything I felt during the time period depicted in the game. Everything flowed, and the key I needed for each lock always seemed to be close at hand.
^
^I often did some pretty random-ass shit with the idea that I was actually solving some kind of puzzle by doing so. There are several examples of this in the game. The problem with making the actual game require these actions is that they were usually absurd and ridiculous and not actually connected to anything in reality. In standard mode, there are relatively few prerequisites for events to trigger, and you can see most of the game just by bouncing around the map like a pingpong ball and doing whatever is obvious. Expert mode creates a more standard weave of 'cant do this until you do that' but the dependencies may not always be 'fair' in traditional adventuring terms.
^
^As for branching contradictory storylines and multiple endings - wouldn't it be nice to have a pony? This is a problem that does bother me, because the idea of free choice within an infinitely branching multiverse is so important to the game's motivations. When I was mentally designing this game for the past couple years, I figured that alternate universes, wide story branching, unrestricted time travel, and lots of other features should be part of it. I think most IF authors are probably nodding and sighing in agreement now.
^
^I've tried to balance the straight line of the core narrative with a huge amount of optional side content and events which can be experienced in a non-linear and freeform way. Hopefully the freedom to see what you want in the game, in what order, is enough player agency to make players feel they are participating in and guiding the story, and there is enough content to make the meta-puzzle of collecting every point a challenge for those who are looking for one. Thanks, and I hope you are enjoying the game.";
];

[ Dodgesub;
	"It doesn't make sense to do that now.";
];

[ Emailsub;
	<<use terminal>>;
];

[ Exitssub;
	location.cant_go();
];

[ Expertsub;
	expertmode = true;
	clockoff = true;
	"Expert-level puzzles and story logic on. Automatic clock events off.";
];

[ Exploresub;
	if (player notin lunar_lightning)
		"You need to be on a bicycle to explore.";
	switch(lunar_lightning.explore) {
	0:	lunar_lightning.explore++;
		"You start your return to bicycle-wandering slow and easy, just riding in random loops around your own neighborhood. There is still something enchanting about quietly moving through the night under the stars. Even just a few blocks from home, you can lose your orientation after a few random turns, and the feeling of not quite knowing which way is north is intriguing...
^
^(As the story progresses and your bike skill increases, you will experience many different things when you >explore.)";
	1:	lunar_lightning.explore++;
		"You ride through the park by your house, over a small wooden bridge across a creek, and then follow a path north, crossing above Washington and Aberg and running beside a golf course toward a technical college. You arc back towards home, recrossing Washington, but going underneath in a tunnel underpass. 'HOOOOOOOON' you sing as you pass through it, the resonant echoes multiplying your voice. The path cuts through a school playground and then under another highway bridge, beside undeveloped land near an old rock quarry adjacent to the farm by your house. A minute later you are back on familiar roads.";
	2:	lunar_lightning.explore++;
		achieved(40);
		"The Jennifer-Spaight neighborhood, close to Lake Monona, is perfect for slow rides. You ride towards downtown by the lakeshore, watching the moonlight rippling on the almost-still water. By Orton park you turn around, and the almost-unnoticeable uphill becomes a slightly downhill, just enough to let you coast at a steady rate of speed. You spark a joint as you slowly roll back towards the east. It is late at night, no one is around, just a trail of smoke puffs in the middle of the street. This will become a traditional way of ending long days of adventuring.";
	3:	if (bikeskill == 0)
			"You enjoy another short ride around the near east side. You will need to increase your biking skill to explore to new places. You can do this by progressing in the story, and/or by drinking a special gelato-espresso drink called an affogato.";
		lunar_lightning.explore++;
		"You feel your body has grown stronger. You can pedal continuously without getting tired now, and no longer dread the hills which ring the city. You bike across the isthmus toward the west side, your childhood neighborhoods. You bike by your old high school and the athletic field nearby, home to countless games of tag and frisbee. You ride down the hill and across University Avenue, towards Picnic Point. You head back towards downtown, passing by the campus buildings you spent so many hours in as a music student.";
	4:	lunar_lightning.explore++;
		"You head north, passing by a field enveloped in soft blue light. It looks so quiet and empty and mysterious you imagine you would just vanish into another universe if you biked into it. You continue north, out to empty streets which have been placed for a residential neighborhood but nothing is yet built. You can almost see into the future, see the suburban homes and strip malls which will be built here. Your ride ends with you hoping that the strange and unpredictable areas of Madison never become bland and homogenous.";
	5:	lunar_lightning.explore++;
		achieved(41);
		"You head due east down Milwaukee street, pushing up the long steady hill which eventually takes it across the interstate to the east. You end up in a sprawling set of new subdivisions, smooth fresh asphalt humming under the tires. As you ride, you notice numerous rabbits darting this way and that in the lawns of the houses. You start using the motion of the rabbits to guide your turns. They take you down numerous shortcuts, sidewalks cutting through the middle of blocks. You feel like pac-man, eating a trail of dots and ringing up a new high score.";
	6:	if (bikeskill < 2)
			"You need to develop even more stamina and endurance to explore new areas on bike.";
		lunar_lightning.explore++;
		playerto(cascade_aerator);
	7:	lunar_lightning.explore++;
		playerto(fish_camp);
	8:	lunar_lightning.explore++;
		print "You eat a generous portion of magic mushrooms, and when you feel the body-buzz start to creep through your limbs, you put a few fruit candies in your pocket for energy and walk towards your bike. It is after three in the morning. As your eyes are tracing the familiar outlines of Orion in the sky, there is a sudden flaring light, and a bright streak splits the sky, fading nearly as quickly as it appeared. Musical fragments are starting to bounce around your head as you pedal into the darkness.^";
		playerto(bunnyworld);
	default: "Another perfect night on your bike, riding wherever the fuck that you like...you return home hours later, tired but happy.";
	}
];

[ Footnotesub;
	switch(noun) {
	1: "Lyrics from Terrapin Station by Robert Hunter, music by Jerry Garcia.";
	2: "There is no footnote 2.";
	3: "You are probably a 17 dimensional chipmunk yourself, you know.";
	4: "Last sentence paraphrased from Fear and Loathing in Las Vegas, 1971 Hunter S. Thompson";
	5: "Was it me? Was it you? I don't know who - but somebody said aroooooo! (Footnote 6)";
	6: "Aroooooooooo!";
	7: "Lyrics from Ripple by Robert Hunter, music by Jerry Garcia.";
	8: "Paraphrased from Tolkien.";
	9: "Lothlorien Co-op! If you had heard only a quarter of what I have heard about it, and I have heard very little of all there is to hear, you would be prepared for any sort of remarkable tale. (Tolkien, of Gandalf)";
	10: "Heinrich Heine, set by Schumann in the first song of Dictherliebe.";
	11: "The Wheel is turning and you can't slow down, you can't let go and you can't hold on, you can't go back and you can't stand still, if the thunder don't get you then the lightning will. (Hunter/Garcia)";
	12: "Amanda Palmer,"; ! what album?
	default: "Probably a special case of the Navier-Stokes equations.";
	};
];

[ Helpsub;
	"If you are new to Interactive Fiction, move around using compass directions (n s e w ne nw se sw up down in out), examine things (x NAME), take things and look in your inventory (take NAME, i), and talk to characters (talk to NAME). A guide: http://inform7.com/learn/eg/dm/IntroductionToIF.pdf
^
^An important verb is the >bind NAME NAME command from the Plan 9 operating system, which forms a conceptual union. Specific hints from the >hint command are available based on your location and progression in the story. You can also type >hint NAME for hints about objects in your vicinity. All required plot-advancing conversations are triggered by >talk to. Characters may have new >talk to responses after story events. Optional conversations are available with >ask NAME about and >tell NAME about. If you are not progressing on your own, the game will automatically trigger some plot events based on an internal timer. You can use >sleep to advance a day, and >story to skip to the next event. >clockoff will deactivate automatic story progression. >clockon to reactivate. The >expert command makes the puzzles and story progression more challenging. >standard will return the game to normal difficulty. >verblist will show all verbs needed to score all points.^";
];

[ Hintsub;
	print "(Different hints are available depending on your location and story progress. hint NAME will give object specific hints for things you can see.)^^";
	if (home.antsreleased == 0)
		"Just type 'release ants' at the > prompt";
	if ((introgoal == 0) || (introgoal == 1)) 
		"During the first part of the introduction, your choices are very limited and the story will advance just by travelling to the available locations using direction commands. (n s e w ne nw se sw out in up down)";
	if ((noun == piano) && (piano.obsessionclear == 0))
		"You need to continue through the introduction, then return home and >play piano.";
	if (noun == piano)
		"You will be able to have new musical experiences and compose new songs many times throughout the game, so try >play piano frequently.";
	if (noun == forseewhy)
		"Once the Afterlife store has opened, you should give 4CY to the owner.";
	if (noun == pendant)
		"Wearing the Loonie Revolution Power Pendant scores 9 points. It has no other function currently.";
	if (noun == lunar_lightning)
		"After you buy Lunar Lightning, you will be able to enter it to ride around and access new areas. You can summon it to your location with the command >lunar if you leave it behind. You can also use the >explore command to experience unique travels. Your bike skill can be increased by drinking an affogato when you find it, and progressing in the story.";
	if (noun == tempest)
		"You can >play tempest multiple times. When you are inside the gameworld, just >bomb to escape with a new high score.";
	if (noun == space_pancakes)
		"The Space Pancakes can be used in a ritual for Hecate at the ritual spot located northeast of the edge of town.";
	if (noun == terminal)
		"The terminal has many functions, but all you need to do is >use terminal when appropriate. You will need to do so multiple times during the game.";
	if (noun == xelia)
		"After you interact with xelia using a command like '>talk to xelia' you should return home and check out your basement. Afterward, you might >tell xelia about xochi";
	if (noun == xochi)
		"After you >talk to xochi, you should >use terminal at home. Also, you might find a gift for her at Astral Enchantments.";
	if (((noun == fiction) || (noun == reality)) && (introgoal == 3))
		"You need to >bind fiction reality.";
	if (((noun == past) || (noun == future)) && (elginbday == 6))
		"You need to >bind past future.";
	if ((noun == balloon) || (noun == catapult))
		"You need to >put balloon in catapult, go to James Madison park, and >use catapult";
	if ((noun == viewer_lens) || (noun == remote_lens))
		"If you leave the remote lens somewhere, abstractions in that location can be bound remotely.";
	if (noun == animal_tarot)
		"If you use the animal tarot in Moon Forest, it will give you access to the Story abstraction. You need to be a strong cyclist to reach it, which requires a visit to the Alchemy Lab underneath Coffee Lion.";
	if ((noun == storybind) || (noun == fortune))
		"The paired abstraction is in a different location. You will need to use the magic lenses from the afterlife to allow the abstractions to be bound together. Visits to Afterlife, Coffee Lion, Turning Wheel, and Moon Forest will be required.";
	if (introgoal == 2)
		"You need to return to Coffee Lion and >talk to xelia.";
	if (introgoal == 3)
		"You need to go to the basement of your home and >bind fiction reality.";
	if ((location == willy_west) && (elginbday == 0))
		"Visiting Lady Wisdom's to the south is probably a good idea.";
	if ((location == willy_west) && (player notin lunar_lightning))
		"You need to buy a bicycle from Turning Wheel and ride it to continue west to downtown. If you have done so, use >lunar to call your bicycle to you.";
	if (location == thedark)
		"Just >sing.";
	if (location == cosmic_wheel)
		"Just >drop spoke.";
	if (location == abandoned_apartment)
		"Just >break screen.";
	if (location == inside_moon)
		"Just >bind orange green.";
	if (location == bunnyworld)
		"Just follow the white rabbits, n, e, then w.";
	if (location == lost_in_grid)
		"Summon >lunar and then move in any direction";
	if (location == atlantis_temple)
		"You need to >bind chaos substance and wait a few turns.";
	if (location == atlantean_academy)
		"The scholars will suggest topics, you just need to >tell scholars about TOPIC.";
	if (location == gelato_alchemy_lab)
		">rub spheres then >push button then >insert cylinder";
	if (location == ritual_spot)
		"You need to bring food here to perform the ritual. You can acquire food by visiting the late-night restaurant nearby a few times.";
	if (location == astral_enchantments)
		"Talking to Chip gives you a valuable item to wear, and buying crystals gives you a gift for Xochi.";
	if ((location == turning_wheel) && (elginbday < 4))
		"After you are invited to Elgin's birthday party, you can buy a bicycle here.";
	if ((location == afterlife_antiques) && (forseewhy notin colette))
		"If you examine the bookshelves in your home, you will receive a book that you should give to Colette.";
	if ((location == atwood_shops) && (introgoal < 4))
		"To unlock the door to Afterlife, you need to go to Elgin's birthday party.";
	if ((location == mon_cot) && (player notin lunar_lightning))
		"To proceed further south, you will need to be invited to Elgin's birthday party, then buy and ride a bicycle found at Turning Wheel cycles.";
	if ((location == miffyland) && (miffyland.summernight == 0))
		"Just >listen.";
	if (elginbday < 2)
		"After completing the introduction, you can visit many new locations to acquire objects and points. Try to go to all the shops, especially Astral Enchantments, Coffee Lion, and Lady Wisdom's coffeeshop,. Try talk to NAME to communicate with characters. You will probably want to interact with the items in your home several times also.";
	if (elginbday < 4)
		"You need to >buy lunar at Turning Wheel cycles, >ride lunar, then go s. s. se. s. in. to arrive at Rossini's pizza for Elgin's birthday party.";
	if (elginbday < 6)
		"After Elgin's party, You need to check your >email at home, then >ride lunar and head to Lothlorien co-op in the northwest corner of downtown.";
	if (elginbday == 6)
		"You need to >bind past future and then go down to the dock.";
	if (elginbday < 10)
		"You should check your >email at home and then >visit staci.";
	"There are many side stories to score additional points. Visit all the locations and interact with all the characters, acquire all the items, and visit your home for >email and to >play piano frequently. >hint OBJECT and asking for a >hint in different locations may provide new information. The current maximum score is 720 points. 84 of those points can be permanently missed if certain optional actions are not performed before plot events.";
];

[ Insertcylsub;
	if(location ~= gelato_alchemy_lab)
		"You don't see that here.";
	move gelato_brew to player;
	remove xochi;
	remove xelia;
	move xelia_and_xochi to coffee_lion;
	gelato_alchemy_lab.brew = 9;
	achieved(32);
	print "^You firmly insert the cylinder into the output junction of the tubes connected to the reaction spheres. Creamy gelato seems to condense at the intersection point and drip slowly down the inner edges of the cylinder. After a moment, it is half filled. Xochi and Xelia each add a shot of freshly brewed espresso to create an affogato.
^
^You hear the sound of the customer service bell being rung upstairs. 'Perfect timing!' they say in unison, then glance at each other and laugh again. You are ushered back upstairs with a tall cylinder of frothy gelato-espresso blended brew.^";
	playerto(coffee_lion);
	return true;
];

[ Lunarsub;
	if (location hasnt outside)
		"You need to be outside for that.";
	if (lunar_lightning.bought == 1) {
		move lunar_lightning to location;
		print "You call out for your trusty metal steed, and it appears!^";
		<<enter lunar_lightning>>;
	}
	"You need to purchase Lunar Lightning at Turning Wheel cycles first.";
];

[ Namespacesub;
	print "Active binds:^";
	if (introgoal > 3)
		print "bind Fiction Reality^";
	if (xochi.email > 3)
		print "bind Schumann self^";
	if (elginbday > 6)
		print "bind Past Future^";
	if (atlantis_temple.chaosbind == true)
		print "bind Chaos Substance^";
	if (fortune.dodge_fate == true)
		print "bind Fortune Story^";
];

[ Petsub;
	"You affectionately stroke the ", (name) noun, " but it doesn't reciprocate your affections.";
];

[ Playsub;
	"Your skills don't extend so far as to be able to play the ", (name) noun;
];

[ Plughsub;
	print "You speak the magic word, and you catch a glimpse of an ancient realm of college mainframe computers and 5 1/4 floppies...^";
	playerto(loth_kitchen);
];

[ Releasesub;
	if (home.antsreleased == 1)
		"You already released ANTS.";
	if (noun ~= ants)
		"What you need to release is ANTS, your Advanced Namespace Tools for Plan 9.";
	Antsrelease_storyline();
];

[ Reviewsub;
	"In the history of independent IF, we have often played games that made us wonder just what, exactly, the author had been smoking. In this game, we find out, and the answer is 'Atlantean cubefruit kif'. The protagonist smokes it while helping a group of students answer oral questions about such subjects as how his bicycle helped him compose the music which accompanies the game.
^
^It's the kind of game where you think the Space Pancakes are going to be given to aliens, but instead you eat half of them and toss the rest into the street as an offering to the Goddess Hecate. It's the kind of game that is so intent on throwing massive walls of half-baked philosophy on the screen that it auto-solves puzzles for you if you wait a few turns. It's the kind of game that decides to parody/borrow from Tolkien, Tron, Schumann, Heinlein, Raphael, Hunter Thompson, Phil Dick, and I assume there is an easter egg where the stay-puft marshmallow man shows up and trashes the city.
^
^What redeems the game from its weaknesses is how strongly it tries to communicate positive emotion and excitement. Forging a bond of empathy with an erratic narrator is a challenge, but in its best moments, the game brings us along with its enthusiasms. Who hasn't imagined our favorite stories becoming real? Would it be worth becoming delusional to perceive life as that magical and meaningful?
^
^The game is clearly grounded in intensely lived experience. It is a flawed work, neither quite as funny nor quite as deep as it hopes to be, but when it succeeds at carrying you along with it, it is quite a ride.";
];

[ Ridesub;
	"You can't ride that.";
];

[ Shootsub;
	"Not a good idea right now.";
];

[ Skipdocksub;
	Antsrelease_storyline();
	print "(You travel to Coffee Lion)^";
	Xochimeet_storyline();
	print "(You travel to the late-night restaurant)^";
	Tarotreading_storyline();
	print "(You travel to Coffee Lion)^";
	coffee_lion.xeliaseen = 2;
	Xeliawardrobe_storyline();
	print "(You find Fiction and Reality linked to your grid, and you bind them together)^";
	Ficbind_storyline();
	print "(You travel to Lady Wisdom's coffeeshop)^";
	Matthewmeet_storyline();
	Bdayinvite_storyline();
	lunar_lightning.bought = 1;
	print "(You go to Elgin's birthday at Rossini's Pizza and Arcade)^^";
	Stacimeet_storyline();
	Bdayparty_storyline();
	Bday_term_storyline();
	print "(You go to Lothlorien co-op to see Staci)^";
	Lothbackyard_storyline();
	print "^(You see future and past here, and bind them together)^";
	Futurebind_storyline();
	Lothdock_storyline();
];

[ Skipintrosub;
	home.antsreleased = 1;
	introgoal = 4;
	piano.obsessionclear = 2;
	move fiction to basement;
	move reality to basement;
	move xochi to coffee_lion;
	"Intro skipped.";
];

[ Standardsub;
	expertmode = false;
	clockoff = false;
	"Game puzzles and story set to standard difficulty. Automatic clock events on.";
];

[ Storysub;
	if(daynumber < 1) {
		settime(1380,60);
		return true;
	}
	if(daynumber < 3) {
		daynumber = 2;
		settime(1380,60);
		return true;
	}
	if(daynumber < 4) {
		daynumber = 3;
		settime(1380,60);
		return true;
	}
	if(daynumber < 6) {
		daynumber = 5;
		settime(1380,60);
		return true;
	}
	if(daynumber < 8) {
		daynumber = 7;
		settime(1380,60);
		return true;
	}
	if(daynumber < 20) {
		daynumber = 19;
		settime(1380,60);
		return true;
	}
	if(daynumber < 28) {
		daynumber = 27;
		settime(1380,60);
		return true;
	}
	if(daynumber < 36) {
		daynumber = 35;
		settime(1380,60);
		return true;
	}
	if(daynumber < 42) {
		daynumber = 41;
		settime(1380, 60);
		return true;
	}
];

[ Sleepwaitsub;
!	daynumber = daynumber + 1;
	settime(1380, 60);
	print "You return home to sleep and drift off, a tourist in a different branch of the multiverse, then resume the next day where you left off.^";
];

[ Talksub;
	if(noun hasnt animate)
		"The soundwaves of your voice bounce off the ", (name) noun, " affecting its molecular structure in subtle ways.";
	"Your words go unanswered.";
];

[ Titlesub;
	"Alternate titles:
^
^Cosmic Delight Rigger volume IX: A new Valisian Testament^
^Joy & Transcendence in Madison: An exuberant journey to the spleen of the Atlantean dream^
^The Big Bluebowski^
^Phase 9^
^The Fool's Progress from this World to that which was Imagined^
^The Moon is a Slutty Submissive Mistress^
^Zurek Penrose Garcia: a transient silver weave";
];

[ Usesub;
	if(noun hasnt animate)
		"Either the ", (name) noun, " can't be used, or you need to find a more specific verb.";
	"To interact with characters, try talk to NAME, ask NAME about THING, tell NAME about THING, or give THING to NAME.";
];

[ Vacationsub;
	print "(In the future)^^You are lucky enough to have an invitation to spend the weekend at a friend's family vacation home up north. After a few hours drive, you arrive in a different world. To return home, use the magic word 'xyzzy.'^^";
	playerto(andrews_cabin);
];

[ Verblistsub;
	"n s e w ne nw se sw u d in out, examine, take, drop, give, talk to, tell, use, play, buy, wear, ride, listen, break, bomb, visit, sing, lunar, explore, release, bind";
];

[ Visitsub;
	if (elginbday == 9) {
		Firstlothvisit_storyline();
		return true;
	}
	if (loth_greatroom.lothstaci >= 3) {
		loth_greatroom.lothstaci++;
		switch(loth_greatroom.lothstaci) {
		4: "You pick Staci up, and decide to spend some time back at your place. You are playing a recording from the Dead's Europe '72 tour, with Pigpen's final performance of 'Turn on your Lovelight'. Pig always brought out the hard-swinging, tightly rhythmic side of the band to support his sexual swagger. Cruising, windows down, confident anticipation of love in the afternoon, blues rock on the car stereo...
^
^Hours later, you drop Staci off, both full of relaxed feelings of mutual affection and deepening trust and respect.";
		5: "A friend has told you that an amazing band will be playing at Yahara Fest today. You travel there, and nibble on some fungus right as the performance is about to begin. You walk through the crowd toward the stage, feeling increasingly electric. 'Please welcome...Climbing the Waltz!'
^
^The stage is now occupied by a time-traveling marching band, performing wild arrangements of original music and doing circus stunts at the same time. Staci and you dance like tripping puppets on strings, and as a rainstorm descends for the final portion of the show, you dance so hard the raindrops vaporize into steam when they land on your gyrating bodies.";
		6:	achieved(43);
			"You have agreed to perform at an anti-corporate personhood event. The Snowden revelations have just come out, and you feel the Revolution has momentum. Staci has agreed to sing a duet on the 'Orange Night Light' song. You go onstage wearing a green bicycle helmet and mirror shades. After chanting some lines about harmonic time-binding the light from the Valispectrum prismatic reflector, you deliver a rant praising Snowden. Staci joins you for your duet, and after you sing together, you spend a few minutes dancing wildly with other co-opers when the next act performs. There is a giddy sense of 'we can just do anything!' that fills you and Staci and Nook and your friends with limitless energy. After, you indulge in some rare alcohol consumption, and break a chair falling over backward. You remember nothing more from that night.";
		default: "You have another in a seemingly endless series of deligtful days with Staci, just chilling on the co-op porch swing and talking with whatever friends wander by.";
		}
	}
	"You don't have anywhere to visit right now.";
];

[ Winsub;
	deadflag = 2;
	"Sure, you win! Success is for you to define. If you want to keep playing, just >undo.";
];

[ Xyzzysub;
	print "There is a trans-dimensional short-circuit of interactive fiction tradition, and you catch a brief glimpse of a brick building near a stream and a metal grate before you blink and find yourself...^";
	playerto(milw_street);
];

!============================================================================
! Grammar and Verbs

Include "Grammar";

Verb "about" *	-> About;
Verb "bind"	* noun noun	-> Bind;
Verb "bomb" *	-> Bomb;
Verb "clockon" *	-> Clockon;
Verb "clockoff" *	-> Clockoff;
Verb "commentary" *	-> Commentary;
Verb "content" *	-> Content;
Verb "credits" *	-> Credits;
Verb "dance" *	-> Dance;
Verb "design" *	-> Design;
Verb "dodge" * 	-> Dodge;
Verb "email" *	-> Email;
Verb "exits" *	-> Exits;
Verb "expert" *	-> Expert;
Verb "explore" *	-> Explore;
Verb "footnote" * number	-> Footnote;
Verb "help" *	-> Help;
Verb "hint" *	-> Hint
	* noun	-> Hint;
Extend "insert" * 'cylinder'	-> Insertcyl;
Verb "lunar" *	-> Lunar;
Verb "namespace" "ns" *	->Namespace;
Verb "pet" * noun	-> Pet;
Verb "play" * noun	-> Play
	* 'with' noun	-> Play;
Verb "plugh" *	-> Plugh;
Verb "release" * noun	-> Release;
Verb "review" *	-> Review;
Verb "ride" * noun	-> Ride;
Verb "shoot" *		-> Shoot;
Verb "skipdock" *	-> Skipdock;
Verb "skipintro" *	-> Skipintro;
Extend "sleep" replace *	-> Sleepwait;
Verb "spin" = 'turn';
Verb "standard" *	-> Standard;
Verb "story" *	-> Story;
Verb "talk" * noun -> Talk
	* 'to'/'with' noun -> Talk;
Verb "titles" *	-> Title;
Verb "use" * noun	-> Use;
Verb "vacation" *	-> Vacation;
Verb "verblist" *	-> Verblist;
Verb "visit" * topic	-> Visit;
Verb "win" *	-> Win;
Verb "xyzzy" *	-> Xyzzy;

